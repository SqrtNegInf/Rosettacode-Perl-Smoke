#!/usr/local/bin/perl
#u# http://rosettacode.org/wiki/Abbreviations,_easy
#c# 2018-09-12 >RC
#p# OK

use strict;
use warnings;
use feature 'say';

my @c = (join ' ', qw<
Add ALTer  BAckup Bottom  CAppend Change SCHANGE  CInsert CLAst COMPress COpy
COUnt COVerlay CURsor DELete CDelete Down DUPlicate Xedit EXPand EXTract Find
NFind NFINDUp NFUp CFind FINdup FUp FOrward GET Help HEXType Input POWerinput
Join SPlit SPLTJOIN  LOAD  Locate CLocate  LOWercase UPPercase  LPrefix MACRO
MErge MODify MOve MSG Next Overlay PARSE PREServe PURge PUT PUTD  Query  QUIT
READ  RECover REFRESH RENum Replace REPeat  CReplace  RESet  RESTore  RGTLEFT
RIght LEft  SAVE  SET SHift SI  SORT  SOS  STAck STATus  TOP TRAnsfer Type Up
>) =~ /([A-Z]+)([a-z]*)(?:\s+|$)/g;

my %abr = ('' => '', ' ' => '');
for (my $i = 0; $i < @c; $i += 2) {
    my $sl = length(my $s =    $c[$i]  );
    my $ll = length(my $l = uc $c[$i+1]);
    $abr{$s} = my $w = $s.$l;
    map { $abr{substr($w, 0, $_)} = $w } $sl .. $ll;
    $abr{$w} = $w; # full command should always work
}

my $fmt = "%-10s";
my $inp = sprintf $fmt, 'Input:';
my $out = sprintf $fmt, 'Output:';
for my $str (' ', <riG rePEAT copies put mo rest types fup. 6 poweRin>) { # DH P::C sees this as 'readline'
    $inp .= sprintf $fmt, $str;
    $out .= sprintf $fmt, $abr{uc $str} // '*error*';
}

print "$inp\n$out\n";

my $ref = 'Output:             RIGHT     REPEAT    *error*   PUT       MOVE      RESTORE   *error*   *error*   *error*   POWERINPUT';

use Test::More;
is($out,$ref);
done_testing;

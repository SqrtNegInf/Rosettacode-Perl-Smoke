#!/usr/local/bin/perl
#u# http://rosettacode.org/wiki/ABC_Problem
#c# 2020-11-04 <RC
#p# OK
#n# Regex based alternate
#n# had to temporarily modify .gitignore to save, 'abc*' rule was blocking

use strict;
use warnings;
use feature 'say';

my @res;

use English;

push @res, sprintf "%30s  %s", $_, can_make_word( $_,
  'BO XK DQ CP NA GT RE TG QD FS JW HU VI AN OB ER FS LY PC ZM' )
  for qw( A BARK BOOK TREAT COMMON SQUAD CONFUSE );

sub can_make_word {
  my ($word, $blocks) = @_;
  my $letter = chop $word or return 'True';
 #can_make_word( $word, $` . $' ) eq 'True' and return 'True'
  can_make_word( $word, $PREMATCH . $POSTMATCH ) eq 'True' and return 'True'
    while $blocks =~ /\w?$letter\w?/gi;
  return 'False';
}

say my $result = join "\n", @res;

my $ref = <<'EOD';
                             A  True
                          BARK  True
                          BOOK  False
                         TREAT  True
                        COMMON  False
                         SQUAD  True
                       CONFUSE  True
EOD

use Test::More;
chomp $ref;
is ($result, $ref);
done_testing;

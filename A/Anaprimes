#!/usr/local/bin/perl
#u# https://rosettacode.org/wiki/Anaprimes
#c# 2023-03-31 >RC
#p# OK
#n# a bit slow with billion, limit testing

my @res;

use v5.36;

use ntheory 'primes';
use List::Util 'max';
use Lingua::EN::Numbers qw(num2en);

my $primes = primes(1,1e8);

for my $l (3..8) {
    my %p;
    $p{ join '', sort split //, "$_" } .= "$_ " for @{ primes 10**($l-1), 10**$l };
    my $m = max map { length $p{$_} } keys %p;
    push @res, sprintf "Largest group of anaprimes before %s: %d members.", num2en(10**$l), $m/($l+1);
    for my $k (sort grep { $m == length $p{$_} } keys %p) {
        push @res, sprintf "First: %d  Last: %d", $p{$k} =~ /^(\d+).* (\d+) $/;
    }
    push @res, '';
}

say my $result = join "\n", @res;

my $ref = <<'END';
Largest group of anaprimes before one thousand: 4 members.
First: 149  Last: 941
First: 179  Last: 971
First: 379  Last: 937

Largest group of anaprimes before ten thousand: 11 members.
First: 1237  Last: 7321
First: 1279  Last: 9721

Largest group of anaprimes before one hundred thousand: 39 members.
First: 13789  Last: 98731

Largest group of anaprimes before one million: 148 members.
First: 123479  Last: 974213

Largest group of anaprimes before ten million: 731 members.
First: 1235789  Last: 9875321

Largest group of anaprimes before one hundred million: 4333 members.
First: 12345769  Last: 97654321

END

#Largest group of anaprimes before one billion: 26519 members.
#First: 102345697  Last: 976542103

use Test::More;
chomp $ref;
is($result, $ref);
done_testing();

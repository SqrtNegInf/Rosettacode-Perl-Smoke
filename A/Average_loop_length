#!/usr/bin/env perl
#u# http://rosettacode.org/wiki/Average_loop_length
#c# 2018-11-09 >RC
#p# OK

srand 123456;

#use warnings;
use strict;
use feature 'say';

my @res;

use List::Util qw(sum reduce);

sub find_loop {
    my($n) = @_;
    my($r,@seen);
    while () { $seen[$r] = $seen[($r = int(1+rand $n))] ? return sum @seen : 1 }
    #while () { $seen[$r] = $seen[($r = int(1+rand $n))] ? return sum grep { defined $_ } @seen : 1 } # to avoid warnings
    #($seen[$r] = $seen[($r = int(1+rand $n))] ? return sum @seen : 1) for 0..$n; # even more obsfuscated!
}

say " N    empiric      theoric     (error)";
say "===  =========  ============  =========";

my $MAX    = 20;
my $TRIALS = 1000;

for my $n (1 .. $MAX) {
    my $empiric = ( sum map { find_loop($n) } 1..$TRIALS ) / $TRIALS;
    my $theoric = sum map { (reduce { $a*$b } $_**2, ($n-$_+1)..$n ) / $n ** ($_+1) } 1..$n;
 
    push @res, sprintf "%3d  %9.4f  %12.4f   (%5.2f%%)",
            $n,  $empiric, $theoric, 100 * ($empiric - $theoric) / $theoric;
}

say my $result = join "\n", @res;

my $ref = <<'END';
  1     1.0000        1.0000   ( 0.00%)
  2     1.4950        1.5000   (-0.33%)
  3     1.9190        1.8889   ( 1.59%)
  4     2.2400        2.2188   ( 0.96%)
  5     2.5120        2.5104   ( 0.06%)
  6     2.7500        2.7747   (-0.89%)
  7     3.0360        3.0181   ( 0.59%)
  8     3.2600        3.2450   ( 0.46%)
  9     3.4440        3.4583   (-0.41%)
 10     3.6670        3.6602   ( 0.19%)
 11     3.8340        3.8524   (-0.48%)
 12     4.0450        4.0361   ( 0.22%)
 13     4.2160        4.2123   ( 0.09%)
 14     4.4420        4.3820   ( 1.37%)
 15     4.5600        4.5458   ( 0.31%)
 16     4.7940        4.7043   ( 1.91%)
 17     4.7830        4.8579   (-1.54%)
 18     4.9140        5.0071   (-1.86%)
 19     5.2490        5.1522   ( 1.88%)
 20     5.2930        5.2936   (-0.01%)
END

use Test::More;
chop $ref;
is($result,$ref);
done_testing;

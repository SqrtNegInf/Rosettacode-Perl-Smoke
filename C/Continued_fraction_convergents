#!/usr/local/bin/perl
#u# https://www.rosettacode.org/wiki/Continued_fraction_convergents
#c# 2024-09-06 <RC
#p# OK

my @res;

use feature 'say';
use strict;
use warnings;

sub convergents {
   my ($x, $maxcount)   = @_;
   my ($epsilon, @comp) = 1e-9;
   for (1..$maxcount) {
      push(@comp, my $ix = int($x)); 
      my $fpart = $x - $ix;
      abs($fpart) < $epsilon ? ( last ) : ( $x = 1 / $fpart )
   }
   my ($numa, $denoma, $numb, $denomb) = ( 1, 0, $comp[0], 1 );
   my @return = ( "$numb/$denomb" ) ;
   for my $i (1 .. $#comp) {
      (  $numa, $denoma, $numb                  , $denomb                     )
      = ($numb, $denomb, $numa + $comp[$i]*$numb, $denoma + $comp[$i]*$denomb );
      push(@return, "$numb/$denomb") 
   }
   @return
}

my @tests = ( 
   [ "415/93", 415/93 ],   [ "649/200", 649/200 ], [ "sqrt(2)", sqrt(2) ], 
   [ "sqrt(5)", sqrt(5) ], [ "golden ratio", (sqrt(5) + 1) / 2 ]
);

push @res, "The continued fraction convergents for the following (maximum 8 terms) are:";
foreach my $test (@tests) {
   my ($s, $x) = @$test;
   push @res, sprintf "%15s = %s", $s, join(' ', convergents($x, 8));
}

say my $result = join "\n", @res;

my $ref = <<'EOD';
The continued fraction convergents for the following (maximum 8 terms) are:
         415/93 = 4/1 9/2 58/13 415/93
        649/200 = 3/1 13/4 159/49 490/151 649/200
        sqrt(2) = 1/1 3/2 7/5 17/12 41/29 99/70 239/169 577/408
        sqrt(5) = 2/1 9/4 38/17 161/72 682/305 2889/1292 12238/5473 51841/23184
   golden ratio = 1/1 2/1 3/2 5/3 8/5 13/8 21/13 34/21
EOD

use Test::More;
chomp $ref;
is ($result, $ref);
done_testing();

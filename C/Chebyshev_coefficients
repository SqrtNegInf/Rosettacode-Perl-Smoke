#!/usr/local/bin/perl
#u# http://rosettacode.org/wiki/Chebyshev_coefficients
#c# 2018-07-22 <RC, 2022-06-13 >RC
#p# OK

my @res;

use strict;
use warnings;
use feature qw{ say signatures };
no warnings qw<experimental::signatures>;

use constant PI => 2 * atan2(1, 0);
 
sub chebft($func, $a, $b, $n) {
  my($bma, $bpa) = ( 0.5*($b-$a), 0.5*($b+$a) );
 
  my @pin = map { ($_ + 0.5) * (PI/$n) } 0..$n-1;
  my @f = map { $func->( cos($_) * $bma + $bpa ) } @pin;
  my @c = (0) x $n;
  for my $j (0 .. $n-1) {
    $c[$j] += $f[$_] * cos($j * $pin[$_])   for 0..$n-1;
    $c[$j] *= (2.0/$n);
  }
  @c;
}
 
push @res, sprintf '%+13.7e', $_ for chebft(sub{cos($_[0])}, 0, 1, 10);

say my $result = join "\n", @res;

my $linux = <<'EOD';
+1.6471695e+00
-2.3229937e-01
-5.3715115e-02
+2.4582353e-03
+2.8211906e-04
-7.7222292e-06
-5.8985565e-07
+1.1521428e-08
+6.5962991e-10
-1.0022017e-11
EOD

my $darwin = <<'EOD';
+1.6471695e+00
-2.3229937e-01
-5.3715115e-02
+2.4582353e-03
+2.8211906e-04
-7.7222292e-06
-5.8985565e-07
+1.1521427e-08
+6.5962992e-10
-1.0021994e-11
EOD

my $ref = `uname` =~ /Darwin/ ? $darwin : $linux;

use Test::More;
chomp $ref;
is ($result, $ref);
done_testing();

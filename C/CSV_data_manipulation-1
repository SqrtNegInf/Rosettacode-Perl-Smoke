#!/usr/local/bin/perl
#u# http://rosettacode.org/wiki/CSV_data_manipulation
#c# 2018-07-19 <>RC
#p# OK

use feature 'say';
my $result;

use warnings;
use strict;

use List::Util 'sum';

my @header = split /,/, <DATA>;
# Remove the newline.
chomp $header[-1];

my %column_number;
for my $i (0 .. $#header) {
    $column_number{$header[$i]} = $i;
}
my @rows   = map [ split /,/ ], <DATA>;
chomp $_->[-1] for @rows;

# Add 1 to the numbers in the 2nd column:
$_->[1]++ for @rows;

# Add C1 into C4:
$_->[ $column_number{C4} ] += $_->[ $column_number{C1} ] for @rows;

# Add sums to both rows and columns.
push @header, 'Sum';
$column_number{Sum} = $#header;

push @$_, sum(@$_) for @rows;
push @rows, [
                map  {
                    my $col = $_;
                    sum(map $_->[ $column_number{$col} ], @rows);
                } @header
            ];

# Print the output.
$result .= sprintf join(',' => @header). "\n";
$result .= sprintf join(',' => @$_). "\n" for @rows;

say $result;

my $ref = <<'END';
C1,C2,C3,C4,C5,Sum
1,6,9,14,17,47
2,7,10,16,18,53
3,8,11,18,19,59
4,9,12,20,20,65
10,30,42,68,74,224
END

use Test::More;
is ($result, $ref);
done_testing;

__END__
C1,C2,C3,C4,C5
1,5,9,13,17
2,6,10,14,18
3,7,11,15,19
4,8,12,16,20

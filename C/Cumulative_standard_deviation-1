#!/usr/local/bin/perl
#u# http://rosettacode.org/wiki/Cumulative_standard_deviation
#c# 2018-07-26 <RC
#p# OK

use strict;
use warnings;
use feature 'say';
my $result;

{ 
    package SDAccum;
    sub new {
	my $class = shift;
	my $self = {};
	$self->{sum} = 0.0;
	$self->{sum2} = 0.0;
	$self->{num} = 0;
	bless $self, $class;
	return $self;
    }
    sub count {
	my $self = shift;
	return $self->{num};
    }
    sub mean {
	my $self = shift;
	return ($self->{num}>0) ? $self->{sum}/$self->{num} : 0.0;
    }
    sub variance {
	my $self = shift;
	my $m = $self->mean;
	return ($self->{num}>0) ? $self->{sum2}/$self->{num} - $m * $m : 0.0;
    }
    sub stddev {
	my $self = shift;
	return sqrt($self->variance);
    }
    sub value {
	my $self = shift;
	my $v = shift;
	$self->{sum} += $v;
	$self->{sum2} += $v * $v;
	$self->{num}++;
	return $self->stddev;
    }
}

my $sdacc = SDAccum->new;
my $sd;

foreach my $v ( 2,4,4,4,5,5,7,9 ) {
    $sd = $sdacc->value($v);
}
say $result = "std dev = $sd";

use Test::More;
is ($result, 'std dev = 2');
done_testing;

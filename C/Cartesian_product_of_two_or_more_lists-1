#!/usr/local/bin/perl
#u# http://rosettacode.org/wiki/Cartesian_product_of_two_or_more_lists
#c# 2018-08-28 >RC
#p# OK
#n# 2023-03-20 changes not on RC

my $result;

use v5.36.0;

sub cartesian($sets) {
    for (@$sets) { return [] unless @$_ }

    my $products = [[]];
    for my $set (reverse @$sets) {
        my @partial = @$products;
        $products = [];
        for my $item (@$set) {
            for my $product (@partial) {
                push @$products, [$item, @$product];
            }
        }
    }
    $products;
}

sub product($s,$fmt) {
    my $tuples = '';
    for $a ( @{ cartesian($s) } ) { $tuples .= sprintf "($fmt) ", @$a; }
    $tuples . "\n";
}

say $result = 
product([[1, 2],      [3, 4]                  ], '%1d %1d'        ).
product([[3, 4],      [1, 2]                  ], '%1d %1d'        ).
product([[1, 2],      []                      ], '%1d %1d'        ).
product([[],          [1, 2]                  ], '%1d %1d'        ).
product([[1,2,3],     [30],   [500,100]       ], '%1d %1d %3d'    ).
product([[1,2,3],     [],     [500,100]       ], '%1d %1d %3d'    ).
product([[1776,1789], [7,12], [4,14,23], [0,1]], '%4d %2d %2d %1d');

my $ref = <<'END';
(1 3) (1 4) (2 3) (2 4) 
(3 1) (3 2) (4 1) (4 2) 


(1 30 500) (1 30 100) (2 30 500) (2 30 100) (3 30 500) (3 30 100) 

(1776  7  4 0) (1776  7  4 1) (1776  7 14 0) (1776  7 14 1) (1776  7 23 0) (1776  7 23 1) (1776 12  4 0) (1776 12  4 1) (1776 12 14 0) (1776 12 14 1) (1776 12 23 0) (1776 12 23 1) (1789  7  4 0) (1789  7  4 1) (1789  7 14 0) (1789  7 14 1) (1789  7 23 0) (1789  7 23 1) (1789 12  4 0) (1789 12  4 1) (1789 12 14 0) (1789 12 14 1) (1789 12 23 0) (1789 12 23 1) 
END

use Test::More;
is ($result, $ref);
done_testing();

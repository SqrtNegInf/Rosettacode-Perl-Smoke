#!/usr/local/bin/perl
#u# http://rosettacode.org/wiki/Chemical_Calculator
#c# 2019-03-20 >RC
#p# OK

use strict;
use warnings;
use feature 'say';
my @res;

use List::Util;
use Parse::RecDescent;

my $g = Parse::RecDescent->new(<<'EOG');
  {
     my %atomic_weight = <H 1.008 C 12.011 O 15.999 Na 22.99 S 32.06>
  }

  weight   : compound         { $item[1] }
  compound : group(s)         { List::Util::sum( @{$item[1]} ) }
  group    : element /\d+/    { $item[1] * $item[2] }
           | element          { $item[1] }
  element  : /[A-Z][a-z]*/    { $atomic_weight{ $item[1] } }
           | "(" compound ")" { $item[2] }
EOG

for (<H H2 H2O Na2SO4 C6H12 COOH(C(CH3)2)3CH3>) {
    push @res, sprintf "%7.3f %s", $g->weight($_), $_;
}

say my $result = join "\n", @res;

my $ref = <<'EOD';
  1.008 H
  2.016 H2
 18.015 H2O
142.036 Na2SO4
 84.162 C6H12
186.295 COOH(C(CH3)2)3CH3
EOD

use Test::More;
chomp $ref;
is($result, $ref);
done_testing;

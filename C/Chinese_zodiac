#!/usr/local/bin/perl
#u# http://rosettacode.org/wiki/Chinese_zodiac
#c# 2018-07-23 <RC
#p# OK
#n# always save file with 'write ++enc=utf-8'

use strict;
use warnings;
use feature 'say';
use utf8;
binmode(STDOUT, ':utf8');

my $result;

sub zodiac {
  my $year = shift;
  my @animals = qw/Rat Ox Tiger Rabbit Dragon Snake Horse Goat Monkey Rooster Dog Pig/;
  my @elements = qw/Wood Fire Earth Metal Water/;
  my @terrestrial_han = qw/子 丑 寅 卯 辰 巳 午 未 申 酉 戌 亥/;
  my @terrestrial_pinyin = qw/zĭ chŏu yín măo chén sì wŭ wèi shēn yŏu xū hài/;
  my @celestial_han = qw/甲 乙 丙 丁 戊 己 庚 辛 壬 癸/;
  my @celestial_pinyin = qw/jiă yĭ bĭng dīng wù jĭ gēng xīn rén gŭi/;
  my @aspect = qw/yang yin/;
 
  my $cycle_year = ($year-4) % 60;
  my($i2, $i10, $i12) = ($cycle_year % 2, $cycle_year % 10, $cycle_year % 12);
 
  ($year,
   $celestial_han[$i10], $terrestrial_han[$i12],
   $celestial_pinyin[$i10], $terrestrial_pinyin[$i12],
   $elements[$i10 >> 1], $animals[$i12], $aspect[$i2], $cycle_year+1);
}
 
$result .= sprintf("%4d: %s%s (%s-%s) %s %s; %s - year %d of the cycle\n", zodiac($_))
  for (1935, 1938, 1968, 1972, 1976, 2017);

say $result; 

my $ref = <<'EOD';
1935: 乙亥 (yĭ-hài) Wood Pig; yin - year 12 of the cycle
1938: 戊寅 (wù-yín) Earth Tiger; yang - year 15 of the cycle
1968: 戊申 (wù-shēn) Earth Monkey; yang - year 45 of the cycle
1972: 壬子 (rén-zĭ) Water Rat; yang - year 49 of the cycle
1976: 丙辰 (bĭng-chén) Fire Dragon; yang - year 53 of the cycle
2017: 丁酉 (dīng-yŏu) Fire Rooster; yin - year 34 of the cycle
EOD

use Test::More;
is ($result, $ref);
done_testing;

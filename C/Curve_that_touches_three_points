#!/usr/local/bin/perl
#u# http://rosettacode.org/wiki/Curve_that_touches_three_points
#c# 2018-12-04 >RC
#t# graphical
#p# OK
#f# RC file: curve-3-points.svg
#n# taking the piss...

use strict;
use warnings;

use SVG;
use List::Util qw(max min);

my(@X,@Y);

use constant pi => 2 * atan2(1, 0);

# Compute the curve with a Lindemayer-system
my %rules = (
    A => '-BF+AFA+FB-',
    B => '+AF-BFB-FA+'
);    
my $hilbert = 'A';
$hilbert =~ s/([AB])/$rules{$1}/eg for 1..6;

# Draw the curve in SVG
my ($x, $y) = (0, 0);
my $theta   = pi/2;
my $r       = 5;

for (split //, $hilbert) {
    if (/F/) {
        push @X, sprintf "%.0f", $x; 
        push @Y, sprintf "%.0f", $y; 
        $x += $r * cos($theta);
        $y += $r * sin($theta);
    }
    elsif (/\+/) { $theta += pi/2; }
    elsif (/\-/) { $theta -= pi/2; }
}

my $max =  max(@X,@Y);
my $xt  = -min(@X)+10;
my $yt  = -min(@Y)+10;
my $svg = SVG->new(width=>$max+20, height=>$max+20);
my $points = $svg->get_path(x=>\@X, y=>\@Y, -type=>'polyline');
$svg->rect(width=>"100%", height=>"100%", style=>{'fill'=>'black'});
$svg->polyline(%$points, style=>{'stroke'=>'orange', 'stroke-width'=>.75}, transform=>"translate($xt,$yt)");
my $fixed = $svg->group( id => 'fixed-points', style => { stroke => 'red', fill => 'red' },);
$fixed->circle( cx =>  10, cy =>  10, r => 1, id => 'point1' );
$fixed->circle( cx => 100, cy => 200, r => 1, id => 'point2' );
$fixed->circle( cx => 200, cy =>  10, r => 1, id => 'point3' );

open my $fh, '>', 'run/curve-3-points.svg';
print $fh  $svg->xmlify(-namespace=>'svg');
close $fh;

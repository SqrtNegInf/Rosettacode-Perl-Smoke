#!/usr/local/bin/perl
#u# http://rosettacode.org/wiki/ISBN13_check_digit
#c# 2019-12-04 >RC
#p# OK
#n# when I was a kid, we didn't have any fancy hyper-operators, but we made do...

use strict;
use warnings;
use feature 'say';

my @res;

sub check_digit {
    my($isbn) = @_; my($sum);
    $sum += (1,3)[$_%2] * (split '', join '', split /\D/, $isbn)[$_] for 0..11;
    (10 - $sum % 10) % 10;
}

for (<978-1734314502 978-1734314509 978-1788399081 978-1788399083 978-2-74839-908-0 978-2-74839-908-5>) {
    my($isbn,$check) = /(.*)(.)/;
    my $check_d = check_digit($isbn);
    push @res, "$_ : " . ($check == $check_d ? 'Good' : "Bad check-digit $check; should be $check_d")
}

say my $result = join "\n", @res;

my $ref = <<'END';
978-1734314502 : Good
978-1734314509 : Bad check-digit 9; should be 2
978-1788399081 : Good
978-1788399083 : Bad check-digit 3; should be 1
978-2-74839-908-0 : Good
978-2-74839-908-5 : Bad check-digit 5; should be 0
END

use Test::More;
chomp $ref;
is($result, $ref);
done_testing;

#!/usr/local/bin/perl
#u# http://rosettacode.org/wiki/Increasing_gaps_between_consecutive_Niven_numbers
#c# 2020-01-13 >RC
#p# OK

use strict;
use warnings;
use feature 'say';

use List::Util 'sum';

my @res;

sub comma { reverse ((reverse shift) =~ s/(.{3})/$1,/gr) =~ s/^,//r }

my ($index, $last, $gap, $count) = (0, 0, 0);
my $threshold = 1_000_000; # 10_000_000;

push @res, 'Gap    Index of gap  Starting Niven';
while (1) {
    $count++;
    next unless 0 == $count % sum split //, $count;
    if ((my $diff = $count - $last) > $gap) { 
        $gap = $diff;
        push @res, sprintf "%3d %15s %15s", $gap, $index > 1 ? comma $index : 1, $last > 1 ? comma $last : 1;
    }
    $last = $count;
    last if ++$index >= $threshold;
}

say my $result = join "\n", @res;

my $ref = <<'END';
Gap    Index of gap  Starting Niven
  1               1               1
  2              10              10
  6              11              12
  7              26              63
  8              28              72
 10              32              90
 12              83             288
 14             102             378
 18             143             558
 23             561           2,889
 32             716           3,784
 36           1,118           6,480
 44           2,948          19,872
 45           4,194          28,971
 54           5,439          38,772
 60          33,494         297,864
 66          51,544         478,764
 72          61,588         589,860
 88          94,748         989,867
 90         265,336       2,879,865
 99         800,054       9,898,956
END
#108       3,750,017      49,989,744
#126       6,292,149      88,996,914

use Test::More;
chomp $ref;
is($result, $ref);
done_testing;

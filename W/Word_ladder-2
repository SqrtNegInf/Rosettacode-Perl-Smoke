#!/usr/local/bin/perl
#u# http://rosettacode.org/wiki/Word_ladder
#c# 2021-11-08 >RC
#p# OK
#n# idiomatification of -1 version (Robbie / translation of C++)

my @res;

use strict;
use warnings;
use feature 'say';

my %dict;
open my $handle, '<', 'ref/unixdict.txt';
#while (chomp(my $word = <$handle>)) { # warning on eof read
while (my $word = <$handle>) {
    chomp $word;
    my $l = length $word;
    if ($dict{$l}) { push @{ $dict{$l} },    $word   }
    else           {         $dict{$l} = \@{[$word]} }
}
close $handle;

sub distance {
    my($w1,$w2) = @_;
    my $d;
    substr($w1, $_, 1) eq substr($w2, $_, 1) or $d++ for 0 .. length($w1) - 1;
    return $d // 0;
}

sub contains {
    my($aref,$needle) = @_;
    $needle eq $_ and return 1 for @$aref;
    return 0;
}

sub word_ladder {
    my($fw,$tw) = @_;
    say 'Nothing like that in dictionary.' and return unless $dict{length $fw};

    my @poss  = @{ $dict{length $fw} };
    my @queue = [$fw];
    while (@queue) {
        my $curr_ref = shift @queue;
        my $last     = $curr_ref->[-1];

        my @next;
        distance($last, $_) == 1 and push @next, $_ for @poss;
        push(@$curr_ref, $tw) and push(@res, join ' -> ', @$curr_ref) and return if contains \@next, $tw;

        for my $word (@next) {
            $word eq $poss[$_] and splice(@poss, $_, 1) and last for 0 .. @poss - 1;
        }
        push @queue, \@{[@{$curr_ref}, $_]} for @next;
    }

    push @res, "Cannot change $fw into $tw";
}

word_ladder(split) for 'boy man', 'girl lady', 'john jane', 'child adult';
#word_ladder(@$_) for ['boy', 'man'], ['girl', 'lady'], ['john', 'jane'], ['child', 'adult'];

say my $result = join "\n", @res;

my $ref = <<'EOD';
boy -> bay -> ban -> man
girl -> gill -> gall -> gale -> gaze -> laze -> lazy -> lady
john -> cohn -> conn -> cone -> cane -> jane
Cannot change child into adult
EOD

use Test::More;
chomp $ref;
is ($result, $ref);
done_testing;

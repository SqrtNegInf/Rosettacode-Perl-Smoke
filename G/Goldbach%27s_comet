#!/usr/local/bin/perl
#u# http://rosettacode.org/wiki/Goldbach%27s_comet
#c# 2022-05-15 >RC
#p# OK
#f# RC file: goldbachs-comet.png
#n# testing text output and image file

my @res;

use strict;
use warnings;
use feature 'say';

use ntheory 'is_prime';
use GD::Graph::bars;
use List::Util <min max>;

sub table { my $t = shift() * (my $c = 1 + max map {length} @_); ( sprintf( ('%'.$c.'s')x@_, @_) ) =~ s/.{1,$t}\K/\n/gr }

sub G {
    my($n) = @_;
    scalar grep { is_prime($_) and is_prime($n - $_) } 2 .. $n/2;
}

my @y; 
push @y, G(2*$_ + 4) for my @x = 0..1999;

# text
push @res, table 10, @y[0..99];
push @res, sprintf "G $_ : %d", G($_) for 1e6;

# graph
my @data = ( \@x, \@y);
my $graph = GD::Graph::bars->new(1200, 400);
$graph->set(
    title          => q/Goldbach's Comet/,
    y_max_value    => 170,
    x_tick_number  => 10,
    r_margin       => 10,
    dclrs          => [ 'blue' ],
) or die $graph->error;
my $gd = $graph->plot(\@data) or die $graph->error;

open my $fh, '>', 'run/goldbachs-comet.png';
binmode $fh;
print $fh $gd->png();
close $fh;

say my $result = join "\n", @res;

my $ref = <<'END';
  1  1  1  2  1  2  2  2  2  3
  3  3  2  3  2  4  4  2  3  4
  3  4  5  4  3  5  3  4  6  3
  5  6  2  5  6  5  5  7  4  5
  8  5  4  9  4  5  7  3  6  8
  5  6  8  6  7 10  6  6 12  4
  5 10  3  7  9  6  5  8  7  8
 11  6  5 12  4  8 11  5  8 10
  5  6 13  9  6 11  7  7 14  6
  8 13  5  8 11  7  9 13  8  9

G 1000000 : 5402
END

use Test::More;
chomp $ref;
is($result, $ref);
done_testing();

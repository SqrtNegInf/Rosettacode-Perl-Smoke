#!/usr/local/bin/perl
#u# http://rosettacode.org/wiki/Text_Completion
#t# skiptest
#c# 2020-08-22 >RC
#p# OK
#n# too much work to handle extended grapheme clusters, Text::Dice can do it...
#n# known bug, fix pending:
#n# https://github.com/Perl/perl5/issues/18123
#n# perl5-32: \K not permitted in lookahead/lookbehind in regex; marked by <-- HERE in m/(?<=\K <-- HERE .)./ at Text_Completion line 19.
#n# The above issue fixed in perl development version, will get to point release eventually, so for now stop testing 

use strict;
use warnings;
use feature 'say';

use Path::Tiny;
use List::Util 'head';

my @res;

# Can't handle Unicode
# Lookbehind longer than 255 not implemented in regex m/(?<=\K\X)\X/ at T/Text_Completion line 16.
# sub bi_gram { (lc shift) =~ /(?<=\K\X)\X/g }

sub bi_gram { (lc shift) =~ /(?<=\K.)./g }

sub score {
    my($phrase, $word) = @_;
    my %count;

    my @match = bi_gram $phrase;
    $count{$_}++ for @match, @$word;
    2 * (grep { $count{$_} > 1 } keys %count) / (@match + @$word);
}

sub sorenson {
    my($dict,$word,$cutoff) = @_; $cutoff //= 0.55;
    my(%matches,$s);

    ($s = score($word, $$dict{$_})) > $cutoff and $matches{$_} = $s for keys %$dict;
    %matches;
}

my %dict = map { $_ => [ bi_gram($_) ] } path('ref/unixdict.txt')->slurp =~ /.{3,}/gm;

for my $word (<complition inconsqual>) { # SÃ¸renson>) {
    my(%scored,@ranked); 

    %scored = sorenson(\%dict,$word);
    push @ranked, sprintf "%.3f $_", $scored{$_} for sort { $scored{$b} <=> $scored{$a} || $a cmp $b } keys %scored;
    push @res, "\n$word:\n" . join("\n", head 10, @ranked);
}

say my $result = join "\n", @res;

my $ref = <<'END';

complition:
0.778 completion
0.737 competition
0.737 composition
0.706 coalition
0.700 incompletion
0.667 complexion
0.667 complicity
0.667 decomposition
0.632 compilation
0.632 compunction

inconsqual:
0.609 inconsequential
0.588 continual
0.571 squall
0.556 conceptual
0.556 continuant
0.556 inconstant
END

use Test::More;
chomp $ref;
is($result, $ref);
done_testing();

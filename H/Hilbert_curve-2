#!/usr/local/bin/perl
#u# http://rosettacode.org/wiki/Hilbert_curve
#c# 2018-11-24 >RC
#t# graphical
#p# OK
#f# RC file: moore_curve.svg

use warnings;
use strict;

use SVG;
use List::Util qw(max min);

my(@X,@Y);

use constant pi => 2 * atan2(1, 0);


# Compute the curve with a Lindemayer-system
my %rules = (
    A => '-BF+AFA+FB-',
    B => '+AF-BFB-FA+'
);
my $moore = 'AFA+F+AFA';
$moore =~ s/([AB])/$rules{$1}/eg for 1..5;

# Draw the curve in SVG
my($x, $y) = (0, 0);
my $theta   = pi/2;
my $r       = 5;

for (split //, $moore) {
    if (/F/) {
        push @X, $x; $x += $r * cos($theta);
        push @Y, $y; $y += $r * sin($theta);
    }
    elsif (/\+/) { $theta += pi/2; }
    elsif (/\-/) { $theta -= pi/2; }
}

my $max =  max(@X,@Y);
my $xt  = -min(@X)+10;
my $yt  = -min(@Y)+10;
my $svg = SVG->new(width=>$max+20, height=>$max+20);
my $points = $svg->get_path(x=>\@X, y=>\@Y, -type=>'polyline');
$svg->rect(width=>"100%", height=>"100%", style=>{'fill'=>'black'});
$svg->polyline(%$points, style=>{'stroke'=>'orange', 'stroke-width'=>1}, transform=>"translate($xt,$yt)");

open my $fh, '>', 'run/moore_curve.svg';
print $fh  $svg->xmlify(-namespace=>'svg');
close $fh;

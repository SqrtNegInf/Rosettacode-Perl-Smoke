#!/usr/local/bin/perl
#u# http://rosettacode.org/wiki/Hofstadter_Q_sequence
#c# 2018-08-13 <RC
#p# OK

my $result;

use strict;
use warnings;
use feature 'say';

package Hofstadter;

sub TIEARRAY {
   bless [undef, 1, 1], shift;
}

sub FETCH {
   my ($self, $n) = @_;
   die if $n < 1;
   if( $n > $#$self ) {
      my $start = $#$self + 1;
      $#$self = $n; # pre-allocate for efficiency
      for my $nn ( $start .. $n ) {
         my ($a, $b) = (1, 2);
         $_ = $self->[ $nn - $_ ] for $a, $b;
         $_ = $self->[ $nn - $_ ] for $a, $b;
         $self->[$nn] = $a + $b;
      }
   }
   $self->[$n];
}

package main;

tie my (@q), "Hofstadter";

$result .= "@q[1..10]\n";
$result .= $q[1000]. "\n";

my $count = 0;
for my $n ( 2 .. 100_000 ) {
   $count++ if $q[$n] < $q[$n - 1];
}
$result .= "Extra credit: $count\n";

say $result;

my $ref = <<'END';
1 1 2 3 3 4 5 5 6 6
502
Extra credit: 49798
END

use Test::More;
is($result, $ref);
done_testing;

#!/usr/local/bin/perl5.32.1
#u# http://rosettacode.org/wiki/Hough_transform
#c# 2018-09-25 >RC, 2019-09-10 >RC
#p# OK
#f# RC file: hough.png

use strict;
use warnings;
use feature 'say';

use Imager;

use constant pi => 2 * atan2(1, 0);
 
sub hough {
    my($im)     = shift;
    my($width)  = shift || 460;
    my($height) = shift || 360;
    my(@canvas);
 
    $height = 2 * int $height/2;
    my($xsize, $ysize) = ($im->getwidth, $im->getheight);
    my $ht = Imager->new(xsize => $width, ysize => $height);
    for my $i (0..$height-1) { for my $j (0..$width-1) { $canvas[$i][$j] = 255 } } 
    $ht->box(filled => 1, color => 'white');
 
    my $rmax = sqrt($xsize**2 + $ysize**2);
    my $dr   = 2 * $rmax / $height;
    my $dth  = pi / $width;
 
    for my $x (0..$xsize-1) {
      for my $y (0..$ysize-1) {
        my $col = $im->getpixel(x => $x, y => $y);
        my($r,$g,$b) = $col->rgba;
        next if $r>120; # && $g==255 && $b==255;
        for my $k (0..$width-1) {
            my $th = $dth*$k;
            my $r = ($x*cos($th) + $y*sin($th));
            my $iry = ($height/2 + int($r/$dr + 0.5));
            $ht->setpixel(x => $k, y => $iry, color => [($canvas[$iry][$k]--) x 3]);
        }
      }
    }
    return $ht;
}
 
my $img = Imager->new;
$img->read(file => 'ref/pentagon.png') or die "Cannot read: ", $img->errstr;
my $ht  = hough($img);
$ht->write(        file => 'run/hough.png');

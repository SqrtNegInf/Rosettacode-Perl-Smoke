#!/usr/local/bin/perl
#u# http://rosettacode.org/wiki/Harshad_or_Niven_series
#c# 2018-08-12 <RC,  2022-09-28 >RC
#p# OK
#n# see -2 for total re-write

use v5.36;

use List::Util 'sum';

sub createHarshads ($limit) {
    my(@harshads,$number);
    do {
        $number++;
        if ( $number % sum ( split ( // , $number ) ) == 0 ) {
            push @harshads , $number;
        }
    } until $harshads[ -1 ] > $limit;
    @harshads
}

my @harshadnumbers = createHarshads my $limit = 1000;
my $result = "@harshadnumbers[0..19]" . "\n";
print $result .= "The first Harshad number greater than $limit is $harshadnumbers[ -1 ]!\n" ;

my $ref = <<'EOD';
1 2 3 4 5 6 7 8 9 10 12 18 20 21 24 27 30 36 40 42
The first Harshad number greater than 1000 is 1002!
EOD

use Test::More;
is ($result, $ref);
done_testing;

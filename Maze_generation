#!/usr/local/bin/perl
#u# http://rosettacode.org/wiki/Maze_generation
#c# 2018-08-18 <RC
#p# OK

srand 123456;

use feature 'say';
my $result;

use List::Util 'max';

#my ($w, $h) = @ARGV;
my ($w, $h) = (20, 10);
$w ||= 26;
$h ||= 127;
my $avail = $w * $h;

# cell is padded by sentinel col and row, so I don't check array bounds
my @cell = (map([(('1') x $w), 0], 1 .. $h), [('') x ($w + 1)]);
my @ver = map([("|  ") x $w], 1 .. $h);
my @hor = map([("+--") x $w], 0 .. $h);

sub walk {
	my ($x, $y) = @_;
	$cell[$y][$x] = '';
	$avail-- or return;	# no more bottles, er, cells

	my @d = ([-1, 0], [0, 1], [1, 0], [0, -1]);
	while (@d) {
		my $i = splice @d, int(rand @d), 1;
		my ($x1, $y1) = ($x + $i->[0], $y + $i->[1]);

		$cell[$y1][$x1] or next;

		if ($x == $x1) { $hor[ max($y1, $y) ][$x] = '+  ' }
		if ($y == $y1) { $ver[$y][ max($x1, $x) ] = '   ' }
		walk($x1, $y1);
	}
}

walk(int rand $w, int rand $h);	# generate

for (0 .. $h) {			# display
	$result .= $_ for @{$hor[$_]}; $result .= "+\n";
	if ($_ < $h) { $result .= $_ for @{$ver[$_]}; $result .= "|\n" }
}

# Run as maze.pl [width] [height] or use default dimensions.

say $result;
my $ref = <<'EOD';
+--+--+--+--+--+--+--+--+--+--+--+--+--+--+--+--+--+--+--+--+
|              |                             |     |        |
+  +--+--+--+--+  +--+--+--+--+--+  +  +--+--+  +  +  +  +  +
|  |        |              |        |  |        |     |  |  |
+  +  +--+  +  +--+--+--+--+  +--+  +--+  +--+--+--+--+  +  +
|     |     |  |              |     |                 |  |  |
+  +--+  +--+  +  +--+--+--+--+  +--+  +--+--+--+  +--+  +  +
|  |  |        |           |  |  |  |  |  |     |  |     |  |
+  +  +--+--+--+--+--+--+  +  +  +  +  +  +  +  +--+  +--+  +
|     |  |              |  |        |  |  |  |     |  |  |  |
+--+  +  +  +--+  +--+  +  +--+--+--+  +  +  +--+  +  +  +  +
|  |  |        |     |  |     |        |  |  |     |  |     |
+  +  +--+--+--+--+  +--+--+  +  +--+--+  +  +  +--+  +--+--+
|  |        |        |        |  |        |  |        |     |
+  +--+--+  +  +--+  +  +--+  +  +  +--+  +  +--+  +--+--+  +
|  |        |     |  |     |  |  |     |  |  |  |  |     |  |
+  +  +--+--+--+  +--+--+  +--+  +--+--+  +  +  +  +  +  +  +
|     |        |        |  |     |        |  |     |  |     |
+  +--+--+  +  +--+--+  +  +  +--+  +--+  +  +--+--+  +--+--+
|           |           |           |     |                 |
+--+--+--+--+--+--+--+--+--+--+--+--+--+--+--+--+--+--+--+--+
EOD

use Test::More;
is ($result, $ref);
done_testing();

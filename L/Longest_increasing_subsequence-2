#!/usr/local/bin/perl
#u# http://rosettacode.org/wiki/Longest_increasing_subsequence
#c# 2018-08-16 <RC
#p# OK

use strict;
use warnings;
use feature 'say';
my $output;

# Patience sorting

sub lis {
    my @pileTops;
    # sort into piles
    foreach my $x (@_) {
	# binary search
	my $low = 0;
    my $high = $#pileTops;
	while ($low <= $high) {
	    my $mid = int(($low + $high) / 2);
	    if ($pileTops[$mid]{val} >= $x) {
	        $high = $mid - 1;
	    } else {
	        $low = $mid + 1;
	    }
	}
	my $i = $low;
	my $node = {val => $x};
        $node->{back} = $pileTops[$i-1] if $i != 0;
	$pileTops[$i] = $node;
    }
    my @result;
    for (my $node = $pileTops[-1]; $node; $node = $node->{back}) {
        push @result, $node->{val};
    }

    return reverse @result;
}

foreach my $r ([3, 2, 6, 4, 5, 1],
	       [0, 8, 4, 12, 2, 10, 6, 14, 1, 9, 5, 13, 3, 11, 7, 15]) {
    my @d = @$r;
    my @lis = lis(@d);
    $output .= "an L.I.S. of [@d] is [@lis]\n";

}

say $output; 
my $ref = <<'EOD';
an L.I.S. of [3 2 6 4 5 1] is [2 4 5]
an L.I.S. of [0 8 4 12 2 10 6 14 1 9 5 13 3 11 7 15] is [0 2 6 9 11 15]
EOD

use Test::More;
is ($output, $ref);
done_testing;

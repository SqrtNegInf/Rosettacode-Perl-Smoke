#!/usr/local/bin/perl
#u# http://rosettacode.org/wiki/Loops/Wrong_ranges
#c# 2018-09-16 >RC
#p# OK

my $result;

use strict;
use warnings;
use feature 'say';

for my $i (
     [ -2,    2,    1], # Normal
     [ -2,    2,    0], # Zero increment
     [ -2,    2,   -1], # Increments away from stop value
     [ -2,    2,   10], # First increment is beyond stop value
     [  2,   -2,    1], # Start more than stop: positive increment
     [  2,    2,    1], # Start equal stop: positive increment
     [  2,    2,   -1], # Start equal stop: negative increment
     [  2,    2,    0], # Start equal stop: zero increment
     [  0,    0,    0], # Start equal stop equal zero: zero increment
) {
    my $iter = gen_seq(@$i);
    $result .= sprintf "start: %3d  stop: %3d  incr: %3d | ", @$i;
    $result .= sprintf "%4s", &$iter for 1..10;
    $result .=  "\n";
}

sub gen_seq {
    my($start,$stop,$increment) = @_;
    my $n = 0;
    return sub { 
        my $term = $start + $n++ * $increment;
        return $term > $stop ? '' : $term;
    }
}

say $result;

my $ref = <<'EOD';
start:  -2  stop:   2  incr:   1 |   -2  -1   0   1   2                    
start:  -2  stop:   2  incr:   0 |   -2  -2  -2  -2  -2  -2  -2  -2  -2  -2
start:  -2  stop:   2  incr:  -1 |   -2  -3  -4  -5  -6  -7  -8  -9 -10 -11
start:  -2  stop:   2  incr:  10 |   -2                                    
start:   2  stop:  -2  incr:   1 |                                         
start:   2  stop:   2  incr:   1 |    2                                    
start:   2  stop:   2  incr:  -1 |    2   1   0  -1  -2  -3  -4  -5  -6  -7
start:   2  stop:   2  incr:   0 |    2   2   2   2   2   2   2   2   2   2
start:   0  stop:   0  incr:   0 |    0   0   0   0   0   0   0   0   0   0
EOD

use Test::More;
is ($result, $ref);
done_testing();

#!/usr/local/bin/perl
#u# http://rosettacode.org/wiki/EKG_sequence_convergence
#c# 2018-10-24 >RC
#p# OK

use strict;
use warnings;
use feature 'say';
my @res;

use List::Util qw(none sum);

sub gcd { my ($u,$v) = @_; $v ? gcd($v, $u%$v) : abs($u) } 
sub shares_divisors_with { gcd( $_[0], $_[1]) > 1 }
 
sub EKG {
    my($n,$limit) = @_;
    my @ekg = (1, $n);
    while (@ekg < $limit) {
        for my $i (2..1e18) {
            next unless none { $_ == $i } @ekg and shares_divisors_with($ekg[-1], $i);
            push(@ekg, $i) and last;
        }
    }
    @ekg;
}

sub converge_at {
    my($n1,$n2) = @_;
    my $max = 100;
    my @ekg1 = EKG($n1,$max);
    my @ekg2 = EKG($n2,$max);
    do { return $_+1 if $ekg1[$_] == $ekg2[$_] && sum(@ekg1[0..$_]) == sum(@ekg2[0..$_])} for 2..$max;
    return "(no convergence in $max terms)";
}
 
push @res, "EKG($_): ". join ' ', EKG($_,10) for 2, 5, 7, 9, 10;
push @res, "EKGs of 5 & 7 converge at term ". converge_at(5, 7);

say my $result = join "\n", @res;

my $ref = <<'EOD';
EKG(2): 1 2 4 6 3 9 12 8 10 5
EKG(5): 1 5 10 2 4 6 3 9 12 8
EKG(7): 1 7 14 2 4 6 3 9 12 8
EKG(9): 1 9 3 6 2 4 8 10 5 15
EKG(10): 1 10 2 4 6 3 9 12 8 14
EKGs of 5 & 7 converge at term 21
EOD
#EKGs of (2 5 7 9 10) converge at term 45

use Test::More;
chop $ref;
is($result, $ref);
done_testing;

#!/usr/local/bin/perl
#u# http://rosettacode.org/wiki/Execute_a_Markov_algorithm
#c# 2018-08-01 <RC
#p# OK

use strict;
use warnings;
use feature 'say';

open my $source, '<ref/rules.markov1';
my @rules;
while (<$source>)
   {/\A#/ and next;
    my @a = /(.*?)\s+->\s+(\.?)(.*)/ or die "Syntax error: $_";
    push @rules, \@a;}
close $source;

my $input = 'I bought a B of As from T S.';

OUTER:
   {for (@rules)
       {my ($from, $terminating, $to) = @$_;
        $input =~ s/\Q$from\E/$to/
            and ($terminating ? last OUTER : redo OUTER);}}
say $input;

use Test::More;
is($input, 'I bought a bag of apples from my brother.');
done_testing;

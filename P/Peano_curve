#!/usr/local/bin/perl
#u# http://rosettacode.org/wiki/Peano_curve
#c# 2018-11-24 >RC
#p# OK
#f# RC file: peano_curve.svg

use strict;
use warnings;
use feature 'say';

use SVG;
use List::Util qw(max min);

use constant pi => 2 * atan2(1, 0);

# Compute the curve with a Lindemayer-system
my %rules = (
    L => 'LFRFL-F-RFLFR+F+LFRFL', 
    R => 'RFLFR+F+LFRFL-F-RFLFR'
);
my $peano = 'L';
$peano =~ s/([LR])/$rules{$1}/eg for 1..4;

# Draw the curve in SVG
my (@X, @Y);
my ($x, $y) = (0, 0);
my $theta   = pi/2;
my $r       = 4;

for (split //, $peano) {
    if (/F/) {
        push @X, sprintf "%.0f", $x; 
        push @Y, sprintf "%.0f", $y; 
        $x += $r * cos($theta);
        $y += $r * sin($theta);
    }
    elsif (/\+/) { $theta += pi/2; }
    elsif (/\-/) { $theta -= pi/2; }
}

my $max =  max(@X, @Y);
my $xt  = -min(@X) + 10;
my $yt  = -min(@Y) + 10;
my $svg = SVG->new(width=>$max+20, height=>$max+20);
my $points = $svg->get_path(x=>\@X, y=>\@Y, -type=>'polyline');
$svg->rect(width=>"100%", height=>"100%", style=>{'fill'=>'black'});
$svg->polyline(%$points, style=>{'stroke'=>'orange', 'stroke-width'=>1}, transform=>"translate($xt,$yt)");

open  my $fh, '>', 'run/peano_curve.svg';
print $fh  $svg->xmlify(-namespace=>'svg');
close $fh;

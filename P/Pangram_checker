#!/usr/local/bin/perl
#u# http://rosettacode.org/wiki/Pangram_checker
#c# 2018-08-20 <RC, 2019-09-10 >RC
#p# OK

use strict;
use warnings;
use feature 'say';

my @res;

sub pangram1 {
    my($str,@set) = @_;
    use List::MoreUtils 'all';
    all {$str =~ /$_/i} @set;
}

sub pangram2 {
    my($str,@set) = @_;
    '' eq (join '',@set) =~ s/[$str]//gir;
}

my @alpha = 'a' .. 'z';
for (
    'Cozy Lummox Gives Smart Squid Who Asks For Job Pen.',
    'Crabby Lummox Gives Smart Squid Who Asks For Job Pen.'
) {
    push @res, pangram1($_,@alpha) ? 'Yes' : 'No';
    push @res, pangram2($_,@alpha) ? 'Yes' : 'No';
}

say my $result = join "\n", @res;

my $ref = <<'END';
Yes
Yes
No
No
END

use Test::More;
chomp $ref;
is ($result, $ref);
done_testing;

#!/usr/local/bin/perl
#u# http://rosettacode.org/wiki/Perfect_shuffle
#c# 2018-08-21 <RC, 2022-08-03 >RC
#p# OK

my $result;

use v5.36;
use List::Util 'all';

sub perfect_shuffle (@deck) {
   my $middle = @deck / 2;
   map { @deck[$_, $_ + $middle] } 0..$middle-1;
}

for my $size (8, 24, 52, 100, 1020, 1024, 10000) {
    my @shuffled = my @deck = 1..$size;
    my $n;
    do { $n++; @shuffled = perfect_shuffle @shuffled }
        until all { $shuffled[$_] == $deck[$_] } 0..$#shuffled;
    $result .= sprintf "%5d cards: %4d\n", $size, $n;
}

say $result; 

my $ref = <<'EOD';
    8 cards:    3
   24 cards:   11
   52 cards:    8
  100 cards:   30
 1020 cards: 1018
 1024 cards:   10
10000 cards:  300
EOD

use Test::More;
is ($result, $ref);
done_testing();

#!/usr/local/bin/perl
#u# http://rosettacode.org/wiki/Permutations/Derangements
#c# 2018-08-21 <RC
#p# OK

use strict;
use warnings;
use feature 'say';
my $result;

#Traditional verbose version[edit]
sub d {
        # compare this with the deranged() sub to see how to turn procedural
        # code into functional one ('functional' as not in 'understandable')
        $#_ ? map d([ @{$_[0]}, $_[$_] ], @_[1 .. $_-1, $_+1 .. $#_ ]),
                        grep { $_[$_] != @{$_[0]} } 1 .. $#_
            : $_[0]
}

sub deranged {  # same as sub d above, just a readable version to explain method
        my ($result, @avail) = @_;
        return $result if !@avail;              # no more elements to pick from, done

        my @list;                               # list of permutations to return
        for my $i (0 .. $#avail) {              # try to add each element to result in turn
                next if $avail[$i] == @$result; # element n at n-th position, no-no
                my $e = splice @avail, $i, 1;   # move the n-th element from available to result
                push @list, deranged([ @$result, $e ], @avail);
                                                # and recurse down, keep what's returned
                splice @avail, $i, 0, $e;       # put that element back, try next
        }
        return @list;
}

sub choose {    # choose k among n, i.e. n! / k! (n-k)!
        my ($n, $k) = @_;
        factorial($n) / factorial($k) / factorial($n - $k)
}

my @fact = (1);
sub factorial {
        # //= : standard caching technique.  If cached value available,
        #       return it; else compute, cache and return.
        #       For this specific task not really necessary.
        $fact[ $_[0] ] //= $_[0] * factorial($_[0] - 1)
}

my @subfact;
sub sub_factorial {
        my $n = shift;
        $subfact[$n] //= do     # same caching stuff, try comment out this line
        {
                # computes deranged without formula, using recursion
                my $total = factorial($n);      # total permutations
                for my $k (1 .. $n) {
                        # minus the permutations where k items are fixed
                        # to original location, and the rest deranged
                        $total -= choose($n, $k) * sub_factorial($n - $k)
                }
                $total
        }
}

$result .= "Derangements for 4 elements:\n";
my @deranged = d([], 0 .. 3);
for (1 .. @deranged) {
        $result .= "$_: @{$deranged[$_-1]}\n"
}

$result .= "\nCompare list length and calculated table\n";
for (0 .. 9) {
        my @x = d([], 0 .. $_-1);
        $result .= $_. "\t". scalar(@x). "\t". sub_factorial($_). "\n"
}

$result .= "\nNumber of derangements:\n";
$result .= "$_:\t". sub_factorial($_). "\n" for 1 .. 20;

say $result;
my $ref = <<'EOD';
Derangements for 4 elements:
1: 1 0 3 2
2: 1 2 3 0
3: 1 3 0 2
4: 2 0 3 1
5: 2 3 0 1
6: 2 3 1 0
7: 3 0 1 2
8: 3 2 0 1
9: 3 2 1 0

Compare list length and calculated table
0	1	1
1	0	0
2	1	1
3	2	2
4	9	9
5	44	44
6	265	265
7	1854	1854
8	14833	14833
9	133496	133496

Number of derangements:
1:	0
2:	1
3:	2
4:	9
5:	44
6:	265
7:	1854
8:	14833
9:	133496
10:	1334961
11:	14684570
12:	176214841
13:	2290792932
14:	32071101049
15:	481066515734
16:	7697064251745
17:	130850092279664
18:	2355301661033953
19:	44750731559645106
20:	895014631192902121
EOD

use Test::More;
is ($result, $ref);
done_testing;

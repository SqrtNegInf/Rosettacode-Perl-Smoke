#!/usr/local/bin/perl5.32.1 # do-not-check
#u# http://rosettacode.org/wiki/Percentage_difference_between_images
#c# 2018-12-08 <RC
#p# OK
#n# on iMac: result is different, adjust testing
#n# 2023-01-19 after modules updates on Mac-Pro, not finding List::SomeUtils?
#n# 2023-03-26 /usr/local/bin/perl5.32.1 /usr/local/bin/cpan List::SomeUtils fixed it

use strict;
use warnings;

use Imager;
use List::AllUtils qw(sum pairwise);
 
sub img_diff {
    my ($file1, $file2) = @_;
 
    my $img1 = Imager->new(file => $file1);
    my $img2 = Imager->new(file => $file2);
 
    my ($w1, $h1) = ($img1->getwidth, $img1->getheight);
    my ($w2, $h2) = ($img2->getwidth, $img2->getheight);
 
    if ($w1 != $w2 or $h1 != $h2) {
        die "Can't compare images of different sizes";
    }
 
    my $sum = 0;
    foreach my $y (0 .. $h1 - 1) {
        foreach my $x (0 .. $w1 - 1) {
            my @rgba1 = $img1->getpixel(x => $x, y => $y)->rgba;
            my @rgba2 = $img2->getpixel(x => $x, y => $y)->rgba;
            $sum += sum(pairwise { abs($a - $b) } @rgba1, @rgba2);
        }
    }
 
    $sum / ($w1 * $h1 * 255 * 3);
}
 
printf "difference = %f%%\n", my $result = 100 * img_diff('ref/Lenna50.jpg', 'ref/Lenna100.jpg');

use Test::More;
like $result, qr/1.78189346213746|1.62559309816049/;
done_testing;

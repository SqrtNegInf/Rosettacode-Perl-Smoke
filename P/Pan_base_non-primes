#!/usr/local/bin/perl
#u# http://rosettacode.org/wiki/Pan_base_non-primes
#c# 2022-09-12 >RC
#p# OK

my @res;

use v5.36;
use ntheory <fromdigits is_prime>;
use List::AllUtils <max firstidx>;

sub table ($c, @V) { my $t = $c * (my $w = 2 + length max @V); ( sprintf( ('%'.$w.'d')x@V, @V) ) =~ s/.{1,$t}\K/\n/gr }

my @np = <4 6 8 9>;

my $max = 2500;

for my $n (11..$max) {
    push @np, $n unless max map { max(split '',$n) < $_ and is_prime fromdigits($n,$_) } 2..$n;
} 

push @res, "First 50 pan-base composites:\n"                        . table 10, @np[0..49];
push @res, "First 20 odd pan-base composites:\n"                    . table 10, (grep { 0 !=  $_ % 2 } @np)[0..19];
push @res, "Count of pan-base composites up to and including $max:" . (1 + (my $f = firstidx { $max <= $_ } @np));
push @res, "Percent odd  up to and including $max: "                . sprintf '%.3f', 100 * scalar(grep { 0 != $_ % 2 } @np[0..$f]) / $f;
push @res, "Percent even up to and including $max: "                . sprintf '%.3f', 100 * scalar(grep { 0 == $_ % 2 } @np[0..$f]) / $f;

say my $result = join "\n", @res;

my $ref = <<'END';
First 50 pan-base composites:
    4    6    8    9   20   22   24   26   28   30
   33   36   39   40   42   44   46   48   50   55
   60   62   63   64   66   68   69   70   77   80
   82   84   86   88   90   93   96   99  100  110
  112  114  116  118  120  121  130  132  134  136

First 20 odd pan-base composites:
    9   33   39   55   63   69   77   93   99  121
  143  165  169  187  231  253  273  275  297  299

Count of pan-base composites up to and including 2500: 953
Percent odd  up to and including 2500: 16.912
Percent even up to and including 2500: 83.193
END

use Test::More;
chomp $ref;
is($result, $ref);
done_testing();

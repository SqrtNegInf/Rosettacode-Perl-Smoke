#!/usr/local/bin/perl
#u# http://rosettacode.org/wiki/Power_set
#c# 2018-08-20 <RC
#p# OK

my $result;

use strict;
use warnings;
use feature 'say';

sub powerset(&@) {
    my $callback = shift;
    my $bitmask = '';
    my $bytes = @_/8;
    {
       my @indices = grep vec($bitmask, $_, 1), 0..$#_;
       $callback->( @_[@indices] );
       ++vec($bitmask, $_, 8) and last for 0 .. $bytes;
       redo if @indices != @_;
    }
}

$result .= "powerset of empty set:\n";
powerset { $result .= "[@_]\n" };
$result .= "powerset of set {1,2,3,4}:\n";
powerset { $result .= "[@_]\n" } 1..4;
my $i = 0;
powerset { ++$i } 1..9;
$result .= "The powerset of a nine element set contains $i elements.\n";


say $result;
my $ref = <<'END';
powerset of empty set:
[]
powerset of set {1,2,3,4}:
[]
[1]
[2]
[1 2]
[3]
[1 3]
[2 3]
[1 2 3]
[4]
[1 4]
[2 4]
[1 2 4]
[3 4]
[1 3 4]
[2 3 4]
[1 2 3 4]
The powerset of a nine element set contains 512 elements.
END

use Test::More;
is ($result, $ref);
done_testing();

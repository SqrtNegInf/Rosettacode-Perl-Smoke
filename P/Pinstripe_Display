#!/usr/local/bin/perl5.32.1
#u# http://rosettacode.org/wiki/Pinstripe/Display
#c# 2018-11-15 >RC
#p# OK
#f# RC file: pinstripes-bw.png

use strict;
use warnings;

use Imager;

my($xsize,$ysize) = (640,400);
my $img = Imager->new(xsize => $xsize, ysize => $ysize);

my $eps = 10**-14;
my $height = int $ysize / 4;
for my $width (1..4) {
    my $stripes = int((1-$eps) + $xsize / $width / 2);
    my @row = ((0) x $width, (1) x $width) x $stripes;
    for my $x (0..$#row) { 
        for my $y (0..$height) { 
            my $offset = $height*($width-1);
            $img->setpixel(x => $x, y => $y+$offset, color => $row[$x] ? 'black' : 'white')
        }
    }
}

$img->write(file => 'run/pinstripes-bw.png');

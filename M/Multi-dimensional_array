#!/usr/local/bin/perl
#u# http://rosettacode.org/wiki/Multi-dimensional_array
#c# 2019-03-21 >RC
#p# OK

my @res;

use strict;
use warnings;
use feature 'say';

# Perl arrays are internally always one-dimensional, but multi-dimension arrays are supported via references.  So a two-dimensional array is an arrays-of-arrays, (with 'rows' that are references to arrays), while a three-dimensional array is an array of arrays-of-arrays, and so on. There are no arbitrary limits on the sizes or number of dimensions (i.e. the 'depth' of nesting references).  

# To generate a zero-initialized 2x3x4x5 array
my @a;
for my $i (0..1) {
  for my $j (0..2) {
    for my $k (0..3) {
        $a[$i][$j][$k] = [(0)x5];
      #for $l (0..4) {
      # $a[$i][$j][$k][$l] = "$i,$j,$k,$l"; # each 'cell' contains it's own address
      #}
    }
  }
}

# There is no requirement that the overall shape of array be regular, or that contents of the array elements
# be of the the same type.  Arrays can contain almost any type of values
my @b = (
     [1, 2, 4, 8, 16, 32],                                                      # numbers
     [<Mon Tue Wed Thu Fri Sat Sun>],                                           # strings
     [sub{$_[0]+$_[1]}, sub{$_[0]-$_[1]}, sub{$_[0]*$_[1]}, sub{$_[0]/$_[1]}]   # coderefs
);
push @res, $b[0][5];           # prints '32'
push @res, $b[1][2];           # prints 'Wed'
push @res, $b[2][0]->(40,2);   # prints '42', sum of 40 and 2

# a bit tricky for RC intro-to-arrays
#@days = \(@{$b[1]});
#say ${$days[1]}; # prints 'Tue'

# Pre-allocation is possible, can result in a more efficient memory layout (in general though Perl allows minimal control over memory)
my @big;
$#big = 1_000;
push @big, 42;
push @res, $#big;

# But dimensions do not need to be pre-declared or pre-allocated. Perl will auto-vivify the necessary storage slots on first access.
my @c;
$c[2][2] = 42;
#    [undef]
#    [undef]
#    [undef, undef, 42]
#use DDP; p @c;

# Negative indicies to count backwards from the end of each dimension

push @res, $c[-1][-1]; # prints '42'

# Elements of an array can be set one-at-a-time or in groups via slices
my @d = <Mon Tue Ned Sat Fri Thu>;
push @d, 'Sun';
$d[2] = 'Wed';
@d[3..5] = @d[reverse 3..5];
push @res, "@d"; # prints 'Mon Tue Wed Thu Fri Sat Sun' 

say my $result = join "\n", @res;

my $ref = <<'END';
32
Wed
42
1001
42
Mon Tue Wed Thu Fri Sat Sun
END

use Test::More;
chomp $ref;
is $result, $ref;
done_testing();

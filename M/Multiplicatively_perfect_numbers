#!/usr/local/bin/perl
#u# https://rosettacode.org/wiki/Multiplicatively_perfect_numbers
#c# 2023-06-13 >RC
#p# OK
#n# similar to B/Blum_integer, and a little janky

my @res;

use v5.36;

use enum    <false true>;
use ntheory <is_prime nth_prime is_semiprime gcd>;

sub comma          { reverse ((reverse shift) =~ s/.{3}\K/,/gr) =~ s/^,//r }
sub table ($c, @V) { my $t = $c * (my $w = 5); ( sprintf( ('%'.$w.'d')x@V, @V) ) =~ s/.{1,$t}\K/\n/gr }

sub find_factor ($n, $constant = 1) {   # NB: n > 1
    my($x, $rho, $factor) = (2, 1, 1);
    while ($factor == 1) {
        $rho *= 2;
        my $fixed = $x;
        for (0..$rho) {
            $x = ( $x * $x + $constant ) % $n;
            $factor = gcd(($x-$fixed), $n);
            last if 1 < $factor;
        }
    }
    $factor = find_factor($n, $constant+1) if $n == $factor;
    $factor
}

# NB: must call with range 1..$limit
sub is_mpn($n) {
    state $cube = 1; $cube = 1 if $n == 1; # set and reset 
    $n == 1 ? return true : is_prime($n) ? return false : ();
    ++$cube, return true if $n == nth_prime($cube)**3;
    my $factor = find_factor($n);
    my $div    = int $n/$factor;
    return true if is_prime $factor and is_prime $div and $div != $factor;
    false
}

push @res, 'Multiplicatively perfect numbers less than 500:';
push @res, table 10, grep { is_mpn $_ } 1..500;

push @res, 'There are:';
for my $limit (5e2, 5e3, 5e4, 5e5, 5e6) { 
    my($m,$s) = (0,0);
    is_mpn       $_ and $m++ for 1..$limit-1;
    is_semiprime $_ and $s++ for 1..$limit-1; 
    push @res, sprintf "%8s MPNs less than %8s, %8s semiprimes", comma($m), $limit, comma $s
}

say my $result = join "\n", @res;

my $ref = <<'END';
Multiplicatively perfect numbers less than 500:
    1    6    8   10   14   15   21   22   26   27
   33   34   35   38   39   46   51   55   57   58
   62   65   69   74   77   82   85   86   87   91
   93   94   95  106  111  115  118  119  122  123
  125  129  133  134  141  142  143  145  146  155
  158  159  161  166  177  178  183  185  187  194
  201  202  203  205  206  209  213  214  215  217
  218  219  221  226  235  237  247  249  253  254
  259  262  265  267  274  278  287  291  295  298
  299  301  302  303  305  309  314  319  321  323
  326  327  329  334  335  339  341  343  346  355
  358  362  365  371  377  381  382  386  391  393
  394  395  398  403  407  411  413  415  417  422
  427  437  445  446  447  451  453  454  458  466
  469  471  473  478  481  482  485  489  493  497

There are:
     150 MPNs less than      500,      153 semiprimes
   1,354 MPNs less than     5000,    1,365 semiprimes
  12,074 MPNs less than    50000,   12,110 semiprimes
 108,223 MPNs less than   500000,  108,326 semiprimes
 978,983 MPNs less than  5000000,  979,274 semiprimes
END

use Test::More;
chomp $ref;
is($result, $ref);
done_testing();

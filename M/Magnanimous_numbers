#!/usr/local/bin/perl
#u# http://rosettacode.org/wiki/Magnanimous_numbers
#c# 2020-04-10 >RC
#p# OK
#n# table hack, like Raku: Â».fmt/batch/join

my @res;

use strict;
use warnings;
use feature 'say';

use ntheory 'is_prime';

sub magnanimous {
    my($n) = @_;
    my $last;
    for my $c (1 .. length($n) - 1) { 
        ++$last and last unless is_prime substr($n,0,$c) + substr($n,$c)
    }
    not $last;
}

my @M;
for ( my $i = 0, my $count = 0; $count < 400; $i++ ) {
    ++$count and push @M, $i if magnanimous($i);
}

push @res, 'First 45 magnanimous numbers';
push @res, (sprintf "@{['%4d' x 45]}", @M[0..45-1]) =~ s/(.{60})/$1\n/gr;

push @res, '241st through 250th magnanimous numbers';
push @res, join ' ', @M[240..249];

push @res, "\n391st through 400th magnanimous numbers";
push @res, join ' ', @M[390..399];

say my $result = join "\n", @res;

my $ref = <<'END';
First 45 magnanimous numbers
   0   1   2   3   4   5   6   7   8   9  11  12  14  16  20
  21  23  25  29  30  32  34  38  41  43  47  49  50  52  56
  58  61  65  67  70  74  76  83  85  89  92  94  98 101 110

241st through 250th magnanimous numbers
17992 19972 20209 20261 20861 22061 22201 22801 22885 24407

391st through 400th magnanimous numbers
486685 488489 515116 533176 551558 559952 595592 595598 600881 602081
END

use Test::More;
chomp $ref;
is($result, $ref);
done_testing;

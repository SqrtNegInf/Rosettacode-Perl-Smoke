#!/usr/local/bin/perl
#u# http://rosettacode.org/wiki/Sum_and_Product_Puzzle
#c# 2018-10-23 >RC
#p# OK

use strict;
use warnings;
use feature 'say';
my $result;

use List::Util qw(none);

sub grep_unique {
    my($by, @list) = @_;
    my @seen;
    for (@list) { 
        my $x = &$by(@$_);
        $seen[$x]= defined $seen[$x] ? 0 : join ' ', @$_;
    }
    grep { $_ } @seen;
}

sub sums {
    my($n) = @_;
    my @sums;
    push @sums, [$_, $n - $_] for 2 .. int $n/2;
    @sums;
}

sub sum     { $_[0] + $_[1] }
sub product { $_[0] * $_[1] }

my(@all_pairs,@s_pairs);

for my $i (2..97) {
    push @all_pairs, map { [$i, $_] } $i + 1..98
}

# Fact 1:
my %p_unique = map { $_ => 1 } grep_unique(\&product, @all_pairs);
for my $p (@all_pairs) {
    push @s_pairs, [@$p] if none { $p_unique{join ' ', @$_} } sums sum @$p;
}
 
# Fact 2:
my @p_pairs = map { [split ' ', $_] } grep_unique(\&product, @s_pairs);
 
# Fact 3:
my @final_pair = grep_unique(\&sum, @p_pairs);

say $result = sprintf "X = %d, Y = %d\n", split ' ', $final_pair[0];

use Test::More;
is($result, "X = 4, Y = 13\n");
done_testing;

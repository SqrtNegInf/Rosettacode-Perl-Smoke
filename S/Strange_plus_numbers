#!/usr/local/bin/perl
#u# http://rosettacode.org/wiki/Strange_plus_numbers
#c# 2021-02-26 >RC
#p# OK

use strict;
use warnings;
use feature 'say';

use ntheory 'is_prime';
use List::Util 'sum';

my($low, $high) = (100, 500);
my $n = my @SP = grep { my @d = split ''; is_prime sum @d[0,1] and is_prime sum @d[1,2] } $low+1 .. $high-1;
#my $n= my @SP = grep { my @d = split ''; is_prime $d[0]+$d[1] and is_prime $d[1]+$d[2] } $low+1 .. $high-1;
say my $result = "Between $low and $high there are $n strange-plus numbers:\n" .
    (sprintf "@{['%4d' x $n]}", @SP[0..$n-1]) =~ s/(.{80})/$1\n/gr;

my $ref = <<'END';
Between 100 and 500 there are 65 strange-plus numbers:
 111 112 114 116 120 121 123 125 129 141 143 147 149 161 165 167 202 203 205 207
 211 212 214 216 230 232 234 238 250 252 256 258 292 294 298 302 303 305 307 320
 321 323 325 329 341 343 347 349 383 385 389 411 412 414 416 430 432 434 438 470
 474 476 492 494 498
END

use Test::More;
chomp $ref;
is($result, $ref);
done_testing();

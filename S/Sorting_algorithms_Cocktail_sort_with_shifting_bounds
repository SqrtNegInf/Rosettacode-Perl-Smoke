#!/usr/local/bin/perl
#u# http://rosettacode.org/wiki/Sorting_algorithms/Cocktail_sort_with_shifting_bounds
#c# 2020-09-06 >RC
#p# OK

#srand 123456;

use strict;
use warnings;
use feature 'say';

sub cocktail_sort {
    my @a = @_;
    my ($min, $max) = (0, $#a-1);
    while (1) {
        my $swapped_forward = 0;
        for my $i ($min .. $max) {
            if ($a[$i] > $a[$i+1]) {
                @a[ $i, $i+1 ] = @a[ $i+1, $i ];
                $swapped_forward = 1
            }
        }
        last if not $swapped_forward;
        $max -= 1;

        my $swapped_backward = 0;
        for my $i (reverse $min .. $max) {
            if ($a[$i] > $a[$i+1]) {
                @a[ $i, $i+1 ] = @a[ $i+1, $i ];
                $swapped_backward = 1;
            }
        }
        last if not $swapped_backward;
        $min += 1;
    }
    @a
}

my @nums;
push @nums, int(rand 100) for 1..25;
say my $result = join ' ', cocktail_sort(@nums);
    my $ref    = join ' ', sort { $a <=> $b } @nums;

use Test::More;
chomp $ref;
is($result, $ref);
done_testing();

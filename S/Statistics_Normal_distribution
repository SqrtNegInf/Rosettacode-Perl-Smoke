#!/usr/local/bin/perl
#u# http://rosettacode.org/wiki/Statistics/Normal_distribution
#c# 2018-09-16 >RC
#p# OK

srand 123456;

use strict;
use warnings;
use feature 'say';
my(@res1,@res2);

use constant pi => 2 * atan2(1, 0);
use List::Util qw(sum reduce min max);

sub normdist {
    my($m, $sigma) = @_;
    my $r = sqrt -2 * log rand;
    my $theta = 2 * pi * rand;
    $r * cos($theta) * $sigma + $m;
}

my $size = 100000; 
my $mean = 50; 
my $stddev = 4;

my(@dataset,%hash);

push @dataset, normdist($mean,$stddev) for 1..$size;
my $m = sum(@dataset) / $size;
push @res1, 'm = ' . $m;

my $sigma = sqrt( (reduce { $a + $b **2 } 0,@dataset) / $size - $m**2 );
push @res1, 'sigma = ' . $sigma;

    $hash{int $_}++ for @dataset;
    my $scale = 180 * $stddev / $size;
    my @subbar = < ⎸ ▏ ▎ ▍ ▌ ▋ ▊ ▉ █ >;
    for my $i (min(@dataset)..max(@dataset)) {
        my $x = ($hash{$i} // 0) * $scale;
        my $full = int $x;
        my $part = 8 * ($x - $full);
        my $t1 = '█' x $full;
        my $t2 = $subbar[$part];
        push @res2, "$i\t$t1$t2";
    }

say for @res1;
say my $result = join "\n", @res2;

my $ref = <<'END';
32	⎸
33	⎸
34	⎸
35	⎸
36	▎
37	▋
38	█▏
39	██▍
40	████▍
41	███████▌
42	████████████⎸
43	███████████████████▏
44	████████████████████████████⎸
45	██████████████████████████████████████▎
46	█████████████████████████████████████████████████▎
47	██████████████████████████████████████████████████████████▋
48	██████████████████████████████████████████████████████████████████▋
49	███████████████████████████████████████████████████████████████████████▍
50	██████████████████████████████████████████████████████████████████████▋
51	██████████████████████████████████████████████████████████████████▌
52	████████████████████████████████████████████████████████████▎
53	████████████████████████████████████████████████▏
54	█████████████████████████████████████▊
55	███████████████████████████▍
56	███████████████████▊
57	████████████▌
58	███████▌
59	████▍
60	██▏
61	█⎸
62	▌
63	▏
64	⎸
65	⎸
66	⎸
END

use Test::More;
is($res1[0], 'm = 49.994568509795');
is($res1[1], 'sigma = 4.00144438401351');
chop $ref;
is($result, $ref);
done_testing;

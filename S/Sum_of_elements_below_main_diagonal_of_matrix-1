#!/usr/local/bin/perl
#u# http://rosettacode.org/wiki/Sum_of_elements_below_main_diagonal_of_matrix
#c# 2021-07-22, 2022-08-31 NOT ON RC
#p# OK

use v5.36;

use List::Util 'sum';

# anonymous array
my $m1 =
[
    [  1,  3,  7,  8, 10 ],
    [  2,  4, 16, 14,  4 ],
    [  3,  1,  9, 18, 11 ],
    [ 12, 14, 17, 18, 20 ],
    [  7,  1,  3,  9,  5 ]
];

my @m2 =
(
    [  1,  3,  7,  8, 10 ],
    [  2,  4, 16, 14,  4 ],
    [  3,  1,  9, 18, 11 ],
    [ 12, 14, 17, 18, 20 ],
    [  7,  1,  3,  9,  5 ]
);

# accepts array ref
sub lower_triangle_sum1 ($m) {
    my $total;
    $total += sum splice @{$m->[$_]}, 0, $_ for 1..$#$m;
    $total;
}

# accepts array
sub lower_triangle_sum2(@m) {
    my @elems;
    push @elems, $m[$_]->@[0..$_-1] for 1..$#m;  # postfix de-ref instead of: @{$m[$_]}[0..$_-1]
    sum @elems;
}

say my $result1 = lower_triangle_sum1($m1);
say my $result2 = lower_triangle_sum2(@m2);
say my $result3 = lower_triangle_sum1(\@m2);

use Test::More;
is($result1, '69');
is($result2, '69');
is($result3, '69');
done_testing();

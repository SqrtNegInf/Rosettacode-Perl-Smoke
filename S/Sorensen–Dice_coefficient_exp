#!/usr/local/bin/perl
#u# https://rosettacode.org/wiki/Sorensenâ€“Dice_coefficient
#c# 2022-11-26
#p# OK
#n# fiddle with case/blanks

my @res;

use strict;
use warnings;
use feature 'say';

use Path::Tiny;
use List::Util <uniq head>;

my $noblanks = 1;

sub bi_gram {
    my $line = shift;
    $line =~ tr/A-Z/a-z/ if $noblanks;
    $line =~ s/ //g if $noblanks;
    uniq map { substr $line,$_,2 } 0..length($line)-2;
}

sub score {
    my($phrase, $word) = @_;
    my %count;
    my @match = bi_gram $phrase;
    $count{$_}++ for @match, @$word;
    2 * (grep { $count{$_} > 1 } keys %count) / (@match + @$word);
}

sub sorensen {
    my($dict,$word,$cutoff) = @_; $cutoff //= 0.00;
    my(%matches,$s);
    ($s = score($word, $$dict{$_})) > $cutoff and $matches{$_} = $s for keys %$dict;
    %matches;
}

my %dict = map { $_ => [ bi_gram($_) ] } path('ref/Sorensen-Dice-Tasks.txt')->slurp =~ /.{10,}/gm;

for my $word ( 'Chowder numbers' ) { #, 'Primordial primes', 'Sunkist-Giuliani formula', 'Sieve of Euripides', 'Chowder numbers'
    my(%scored,@ranked); 
    if ($noblanks) {
        %scored = sorensen(\%dict,(lc $word) =~ s/ //gr);
    } else {
        %scored = sorensen(\%dict,$word);
    }
    push @ranked, sprintf "%.3f $_", $scored{$_} for sort { $scored{$b} <=> $scored{$a} || $a cmp $b } keys %scored;
    push @res, "\n$word:\n" . join("\n", head 5, @ranked);
}

say my $result = join "\n", @res;

my $ref = <<'END';

Chowder numbers:
0.750 Chowla numbers
0.583 Rhonda numbers
0.571 Bell numbers
0.571 Lah numbers
0.560 Bernoulli numbers
END

use Test::More;
chomp $ref;
is($result, $ref);
done_testing();

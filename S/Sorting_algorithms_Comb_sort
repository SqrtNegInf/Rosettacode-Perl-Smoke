#!/usr/local/bin/perl
#u# http://rosettacode.org/wiki/Sorting_algorithms/Comb_sort
#c# 2018-08-15 <RC
#p# OK

srand 5; # fragile!

use strict;
use warnings;
use feature 'say';

sub combSort {
    my @arr = @_;
    my $gap = @arr;
    my $swaps = 1;
    while ($gap > 1 || $swaps) {
        $gap /= 1.25 if $gap > 1;
        $swaps = 0;
        foreach my $i (0 .. $#arr - $gap) {
            if ($arr[$i] > $arr[$i+$gap]) {
                @arr[$i, $i+$gap] = @arr[$i+$gap, $i];
                $swaps = 1;
            }
        }
    }
    return @arr;
}

use Test::More;
say 'input: ' . join ' ', my @a = map (int rand 100), 0 .. 10;
is_deeply([combSort(@a)], [sort { $a <=> $b } @a]);
done_testing;

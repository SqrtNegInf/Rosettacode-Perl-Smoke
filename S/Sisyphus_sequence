#!/usr/local/bin/perl
#u# https://rosettacode.org/wiki/Sisyphus_sequence
#c# 2023-06-11 >RC
#p# OK
#n# 3..8:  334.28s user  0.61s system 99% cpu    5:36 
#n# 3..8:  296.92s user  0.52s system 99% cpu    4:57 with 'use integer'
#n# 3..9: 3419.67s user 40.60s system 97% cpu 1:25:03        "

my @res;

use v5.36;

use ntheory 'next_prime';
use List::Util <any max>;
use integer;  # a little faster

sub comma { reverse ((reverse shift) =~ s/.{3}\K/,/gr) =~ s/^,//r }
sub table { my $t = 10 * (my $c = 1 + length max @_); ( sprintf( ('%'.$c.'d')x@_, @_) ) =~ s/.{1,$t}\K/\n/gr }

my ($exp1, $exp2, $limit1, $limit2) = (3, 5, 100, 250);
my ($n, $s0, $s1, $p, @S1, %S, %S2) = (1, 1, 0, 1, 1);
my @Nth = map { 10**$_ } $exp1..$exp2;

do {
    $n++;
    $s1 = (0 == $s0%2) ? $s0/2 : $s0 + ($p = next_prime($p));
    push @S1, $s1 if  $n <= $limit1;
    $S2{$s1}++    if $s1 <= $limit2;
    ($S{$n}{'value'} = $s1 and $S{$n}{'prime'} = $p) if any { $_ == $n } @Nth;
    $s0 = $s1;
} until $n == $Nth[-1];

push @res, 'The first 100 members of the Sisyphus sequence are:';
push @res, table @S1;

push @res, sprintf '%12s member is: %10s with prime: %10s', comma($_), comma($S{$_}{value}), comma($S{$_}{prime}) for @Nth;

push @res, sprintf "\nNumbers under $limit2 that do not occur in the first %s terms:", comma $Nth[-1];
push @res, join ' ', grep { ! defined $S2{$_} } 1..$limit2;

my $max = max values %S2;
push @res, sprintf "\nNumbers under $limit2 occur the most ($max times) in the first %s terms:", comma $Nth[-1];
push @res, join ' ', sort { $a <=> $b } grep { $S2{$_} == $max } keys %S2;

say my $result = join "\n", @res;

my $ref = <<'END';
The first 100 members of the Sisyphus sequence are:
   1   3   6   3   8   4   2   1   8   4
   2   1  12   6   3  16   8   4   2   1
  18   9  28  14   7  30  15  44  22  11
  42  21  58  29  70  35  78  39  86  43
  96  48  24  12   6   3  62  31  92  46
  23  90  45 116  58  29 102  51 130  65
 148  74  37 126  63 160  80  40  20  10
   5 106  53 156  78  39 146  73 182  91
 204 102  51 178  89 220 110  55 192  96
  48  24  12   6   3 142  71 220 110  55

       1,000 member is:        990 with prime:      2,273
      10,000 member is:     24,975 with prime:     30,713
     100,000 member is:    265,781 with prime:    392,111

Numbers under 250 that do not occur in the first 100,000 terms:
27 36 54 60 72 97 107 108 115 120 127 144 157 167 184 189 194 195 211 212 214 216 223 230 231 232 239 240

Numbers under 250 occur the most (6 times) in the first 100,000 terms:
3 57 114 228
END

use Test::More;
chomp $ref;
is($result, $ref);
done_testing;

#!/usr/local/bin/perl
#u# http://rosettacode.org/wiki/Sparkline_in_unicode
#c# 2018-08-14 <RC, 2019-02-24 <RC
#p# OK
#f# RC file: sparkline.txt
#n# not incorporated from RC: extreme ranges and multiple output styles

use warnings;
use strict;

my $result;

use open ':std', ':encoding(utf8)';
#binmode(STDOUT, ':utf8');

our @sparks=map { chr } 0x2581..0x2588;

sub sparkline(_) {
    my @n=map {0+$_} grep {length} split /[\s,]+/,shift or return '';
    my($min,$max)=($n[0])x2;
    if (@n>1) {
        for (@n[1..$#n]) {
            if    ($_<$min) { $min=$_ }
            elsif ($_>$max) { $max=$_ }
        }
    }
    my $sparkline='';
    for(@n) {
        my $height=int( $max==$min ? @sparks/2 : @sparks * ($_-$min)/($max-$min) );
        $height=$#sparks if $height>$#sparks;
        $sparkline.=$sparks[$height];
    }
    $result .= sprintf "%d values; range %s..%s\n", scalar(@n), $min, $max;
    $result .= $sparkline."\n";
}

sparkline('1 2 3 4 5 6 7 8 7 6 5 4 3 2 1');
sparkline('1.5, 0.5 3.5, 2.5 5.5, 4.5 7.5, 6.5');
sparkline('3 2 1 0 -1 -2 -3 -4 -3 -2 -1 0 1 2 3');
sparkline('12 12 12 12');

print "$result\n"; 

open F, '>', 'run/sparkline.txt';
print F $result; 

my $ref = <<'EOD';
15 values; range 1..8
▁▂▃▄▅▆▇█▇▆▅▄▃▂▁
8 values; range 0.5..7.5
▂▁▄▃▆▅█▇
15 values; range -4..3
█▇▆▅▄▃▂▁▂▃▄▅▆▇█
4 values; range 12..12
▅▅▅▅
EOD

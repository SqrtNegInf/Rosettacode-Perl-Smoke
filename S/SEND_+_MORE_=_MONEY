#!/usr/local/bin/perl
#u# https://rosettacode.org/wiki/SEND_+_MORE_=_MONEY
#c# 2023-03-19 >RC
#p# OK
#n# tweaks for speed not on RC

my @res;

use v5.36;

use enum <D E M N O R S Y>;
use Algorithm::Combinatorics <combinations permutations>;

sub find_solution {
  for my $p (map { permutations $_ } combinations [reverse 0..9], 8) {
    next if @$p[M] == 0 or @$p[S] < 8;
    return $p if join('',@$p[S,E,N,D]) + join('',@$p[M,O,R,E]) == join('',@$p[M,O,N,E,Y]);
  }
}

my $s = find_solution();
push @res, 'SEND + MORE == MONEY';
push @res, sprintf "%d + %d == %d", join('',@$s[S,E,N,D]), join('',@$s[M,O,R,E]), join '',@$s[M,O,N,E,Y];

say my $result = join "\n", @res;

my $ref = <<'END';
SEND + MORE == MONEY
9567 + 1085 == 10652
END

use Test::More;
chomp $ref;
is($result, $ref);
done_testing();

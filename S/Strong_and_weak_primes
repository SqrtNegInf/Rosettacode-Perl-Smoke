#!/usr/local/bin/perl
#u# http://rosettacode.org/wiki/Strong_and_weak_primes
#c# 2018-12-05 >RC
#p# OK

use strict;
use warnings;
use feature 'say';
my @res;

use ntheory qw(primes);

sub comma { reverse ((reverse shift) =~ s/(.{3})/$1,/gr) =~ s/^,//r }

sub below { my($m,@a) = @_; my $c = 0; while () { return $c if $a[++$c] > $m } }
 
my(@weak,@strong,@balanced);
my @primes = @{ primes(10_000_019) };

for my $p (1 .. $#primes - 1) {
    my $x = ($primes[$p - 1] + $primes[$p + 1]) / 2;
    if    ($x > $primes[$p]) { push @weak,     $primes[$p] }
    elsif ($x < $primes[$p]) { push @strong,   $primes[$p] }
    else                     { push @balanced, $primes[$p] }
}

for ([\@strong,   'strong',   36], 
     [\@weak,     'weak',     37],
     [\@balanced, 'balanced', 28]) {
    my($pr, $type, $d) = @$_;
    push @res, "\nFirst $d $type primes:", join ' ', map { comma $_ } @$pr[0..$d-1];
    push @res, "Count of $type primes <=  @{[comma 1e6]}:  " . comma below(1e6,@$pr); 
    push @res, "Count of $type primes <= @{[comma 1e7]}: "   . comma scalar @$pr;
}

say my $result = join "\n", @res;

my $ref = <<'EOD';

First 36 strong primes:
11 17 29 37 41 59 67 71 79 97 101 107 127 137 149 163 179 191 197 223 227 239 251 269 277 281 307 311 331 347 367 379 397 419 431 439
Count of strong primes <=  1,000,000:  37,723
Count of strong primes <= 10,000,000: 320,991

First 37 weak primes:
3 7 13 19 23 31 43 47 61 73 83 89 103 109 113 131 139 151 167 181 193 199 229 233 241 271 283 293 313 317 337 349 353 359 383 389 401
Count of weak primes <=  1,000,000:  37,780
Count of weak primes <= 10,000,000: 321,750

First 28 balanced primes:
5 53 157 173 211 257 263 373 563 593 607 653 733 947 977 1,103 1,123 1,187 1,223 1,367 1,511 1,747 1,753 1,907 2,287 2,417 2,677 2,903
Count of balanced primes <=  1,000,000:  2,994
Count of balanced primes <= 10,000,000: 21,837
EOD

use Test::More;
chop $ref;
is($result, $ref);
done_testing();

#!/usr/local/bin/perl
#u# http://rosettacode.org/wiki/Show_Ascii_table
#c# 2018-09-03 >RC
#p# OK
#f# RC file: ASCII_wiki.txt
#n# this is what I uploaded to RC

# Alternately, and perhaps more usefully, output as a wiki-table rather than ASCII art. Hover mouse over the glyph to get the name.

use strict;
use warnings;

use charnames ':full';

sub glyph {
    my($n) = @_;
    if    ($n < 33) { chr 0x2400 + $n } # display symbol names for invisible glyphs
    elsif ($n==124) { '<nowiki>|</nowiki>' }
    elsif ($n==127) { 'DEL' }
    else            { chr $n }
}

my $result = qq[{|class="wikitable" style="text-align:center;background-color:hsl(39, 90%, 95%)"\n];

for (0..127) {
    $result .= qq[|-\n] unless $_ % 16;
    $result .= sprintf qq[|%d<br>0x%02X<br><big><big title="%s">%s</big></big>\n], $_, $_, charnames::viacode($_), glyph($_);
}
$result .= qq[|}\n];

use open ":encoding(utf8)";
open F, '>run/ASCII_wiki.txt';
print F $result;

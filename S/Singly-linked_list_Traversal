#!/usr/local/bin/perl
#u# http://rosettacode.org/wiki/Singly-linked_list/Traversal
#c# 2018-08-16 <RC
#p# OK

use feature 'say';
my $result;

# We use Class::Tiny to get OO functionality with minimal effort.

package SSL_Node;
use strict;
use Class::Tiny qw( val next );

sub BUILD {
    my $self = shift;
    exists($self->{val}) or die "Must supply 'val'";
    if (exists $self->{next}) {
        ref($self->{next}) eq 'SSL_Node'
            or die "If supplied, 'next' must be an SSL_Node";
    }
    return;
}

package main;
use strict;
# Construct an example list,
my @vals = 1 .. 10;
my $countdown = SSL_Node->new(val => shift(@vals));
while (@vals) {
    my $head = SSL_Node->new(val => shift(@vals), next => $countdown);
    $countdown = $head;
}
# ...then traverse it.
my $node = $countdown;
while ($node) {
    $result .= $node->val . "... ";
    $node = $node->next;
}

say $result;

use Test::More;
is ($result, '10... 9... 8... 7... 6... 5... 4... 3... 2... 1... ');
done_testing;

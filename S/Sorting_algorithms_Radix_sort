#!/usr/local/bin/perl
#u# http://rosettacode.org/wiki/Sorting_algorithms/Radix_sort
#c# 2018-08-15 <RC
#p# OK

use strict;
use warnings;
use feature 'say';

# Radix sort in base 10.

sub radix {
    my @tab = ([@_]);

    my $max_length = 0;
    length > $max_length and $max_length = length for @_;
    $_ = sprintf "%0${max_length}d", $_ for @{ $tab[0] }; # Add zeros.

    for my $pos (reverse -$max_length .. -1) {
        my @newtab;
        for my $bucket (@tab) {
            for my $n (@$bucket) {
                my $char = substr $n, $pos, 1;
                $char = -1 if '-' eq $char;
                $char++;
                push @{ $newtab[$char] }, $n;
            }
        }
        @tab = @newtab;
    }

    my @return;
    my $negative = shift @tab;                            # Negative bucket must be reversed.
    push @return, reverse @$negative if $negative;
    for my $bucket (@tab) {
        push @return, @{ $bucket // [] };
    }
    $_ = 0 + $_ for @return;                              # Remove zeros.
    return @return;
}

use Test::More;
say 'input: ' . join ' ', my @a = map (int rand 100), 0 .. 50;
is_deeply([radix(@a)], [sort { $a <=> $b } @a]);
done_testing();

#!/usr/local/bin/perl
#u# http://rosettacode.org/wiki/Knapsack_problem/Bounded
#c# 2018-08-15 <RC
#p# OK
#n# recursive solution with caching (minor changes, not on RC)
#n# too-clever-by-half cache-update code doesn't translate easily to v5.36

my $result;

use strict;
use warnings;
use feature 'say';

my $raw = <<'TABLE';
map     	9       150     1
compass 	13      35      1
water   	153     200     2
sandwich        50      60      2
glucose 	15      60      2
tin     	68      45      3
banana  	27      60      3
apple  		39      40      3
cheese  	23      30      1
beer    	52      10      1
suntancream     11      70      1
camera  	32      30      1
T-shirt 	24      15      2
trousers        48      10      2
umbrella        73      40      1
w_trousers     	42      70      1
w_overcoat  	43      75      1
note-case       22      80      1
sunglasses      7       20      1
towel   	18      12      2
socks   	4       50      1
book    	30      10      2
TABLE

my @items;
for (split "\n", $raw) {
    for ([ split /\s+/ ]) {
        push @items, {
            name   => $_->[0],
            weight => $_->[1],
            value  => $_->[2],
            quant  => $_->[3],
        }
    }
}

my $max_weight = 400;

my %cache;
sub pick {
	my ($weight, $pos) = @_;
	return 0, 0, [] if $pos < 0 or $weight <= 0;

	@{ $cache{$weight . $pos} //= [do{	# odd construct: for caching
		my $item = $items[$pos];
		my ($bv, $bi, $bw, $bp) = (0, 0, 0, []);

		for my $i (0 .. $item->{quant}) {
			last if $i * $item->{weight} > $weight;
			my ($v, $w, $p) = pick($weight - $i * $item->{weight}, $pos - 1);
			next if ($v += $i * $item->{value}) <= $bv;

			($bv, $bi, $bw, $bp) = ($v, $i, $w, $p);
		}

		my @picked = ( @$bp, $bi );
		$bv, $bw + $bi * $item->{weight}, \@picked
	}]}
}

my ($v, $w, $p) = pick($max_weight, $#items);
for (0 .. $#$p) {
	if ($p->[$_] > 0) {
		$result .= "$p->[$_] of $items[$_]{name}\n";
	}
}
$result .= "Value: $v; Weight: $w\n";

say $result; 

my $ref = <<'EOD';
1 of map
1 of compass
1 of water
2 of glucose
3 of banana
1 of cheese
1 of suntancream
1 of w_overcoat
1 of note-case
1 of sunglasses
1 of socks
Value: 1010; Weight: 396
EOD

use Test::More;
is ($result, $ref);
done_testing;

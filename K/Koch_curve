#!/usr/local/bin/perl
#u# http://rosettacode.org/wiki/Koch_curve
#c# 2018-11-24 >RC
#t# graphical
#p# OK
#f# RC file: koch_curve.svg

use strict;
use warnings;

use SVG;
use List::Util qw(max min);

use constant pi => 2 * atan2(1, 0);

# Compute the curve with a Lindemayer-system
my $koch = 'F--F--F';
$koch =~ s/F/F+F--F+F/g for 1..5;

# Draw the curve in SVG
my (@X, @Y);
my ($x, $y) = (0, 0);
my $theta  = pi/3;
my $r      = 2;

for (split //, $koch) {
    if (/F/) {
        push @X, sprintf "%.0f", $x; 
        push @Y, sprintf "%.0f", $y; 
        $x += $r * cos($theta);
        $y += $r * sin($theta);
    }
    elsif (/\+/) { $theta += pi/3; }
    elsif (/\-/) { $theta -= pi/3; }
}

my ($xrng, $yrng) = ( max(@X) - min(@X),  max(@Y) - min(@Y));
my ($xt,   $yt)   = (-min(@X) + 10,      -min(@Y) + 10);
my $svg = SVG->new(width=>$xrng+20, height=>$yrng+20);
my $points = $svg->get_path(x=>\@X, y=>\@Y, -type=>'polyline');
$svg->rect(width=>"100%", height=>"100%", style=>{'fill'=>'black'});
$svg->polyline(%$points, style=>{'stroke'=>'orange', 'stroke-width'=>1}, transform=>"translate($xt,$yt)");

open my $fh, '>', 'run/koch_curve.svg';
print $fh  $svg->xmlify(-namespace=>'svg') =~ s/-0/0/gr;
close $fh;

#!/usr/local/bin/perl
#u# http://rosettacode.org/wiki/File_extension_is_in_extensions_list
#c# 2018-08-04 <RC
#p# OK

my $result;

use strict;
use warnings;
use feature 'say';

sub check_extension {
    my ($filename, @extensions) = @_;
    my $extensions = join '|', map quotemeta, @extensions;
    scalar $filename =~ / \. (?: $extensions ) $ /xi
}

my @extensions = ('zip', 'rar', '7z', 'gz', 'archive', 'A##', 'tar.bz2');

my @files= (
    'MyData.a##', 'MyData.tar.Gz', 'MyData.gzip', 'MyData.7z.backup',
    'MyData...', 'MyData', 'MyData_v1.0.tar.bz2', 'MyData_v1.0.bz2'
);

$result .= sprintf "%-19s - %s\n",
    $_, check_extension($_, @extensions) ? 'true' : 'false'
    for @files;

say $result; 

my $ref = <<'EOD';
MyData.a##          - true
MyData.tar.Gz       - true
MyData.gzip         - false
MyData.7z.backup    - false
MyData...           - false
MyData              - false
MyData_v1.0.tar.bz2 - true
MyData_v1.0.bz2     - false
EOD

use Test::More;
is ($result, $ref);
done_testing;

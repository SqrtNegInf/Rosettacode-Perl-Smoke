#!/usr/local/bin/perl
#n# NOT CURRENT, NOT SMOKE-TESTED
#u# http://rosettacode.org/wiki/First_perfect_square_in_base_N_with_N_unique_digits
#c# 2019-05-21 >RC
#p# OK
#n# fine for 2..15 (with 13 taking most of the time), but 16 requires bigint, which would be super slow

use strict;
use warnings;
use feature 'say';
my @res;
use bigint try => 'GMP,Pari';

use ntheory 'todigitstring';

sub first_square {
    my($n) = @_;
    my $sq;
    my $r = int($n**(($n-1)/2)) || 1;
    my @digits = split '', substr('0123456789abcdef',0,$n);
    TRY: while (1) {
        $sq = $r++**2;
        my $cnt = 0;
        my $s = todigitstring($sq, $n);
        for (@digits) {
            redo TRY if -1 == index($s,$_);
            last TRY if ++$cnt == $n;
        }
    }
    sprintf "Base %2d: %10s² == %s", $n, todigitstring(sqrt($sq),$n), todigitstring($sq, $n);
}

# a bit slower
#        map { $seen{$_}++ } split '', todigitstring($sq, $n);
#        last if (keys %seen) == $n;

say "First perfect square with N unique digits in base N: ";
push @res, first_square($_) for 2..15;

say my $result = join "\n", @res;

my $ref = <<'EOD';
Base  2:         10² == 100
Base  3:         22² == 2101
Base  4:         33² == 3201
Base  5:        243² == 132304
Base  6:        523² == 452013
Base  7:       1431² == 2450361
Base  8:       3344² == 13675420
Base  9:      11642² == 136802574
Base 10:      32043² == 1026753849
Base 11:     111453² == 1240a536789
Base 12:     3966b9² == 124a7b538609
Base 13:    3828943² == 10254773ca86b9
Base 14:    3a9db7c² == 10269b8c57d3a4
Base 15:   1012b857² == 102597bace836d4
EOD
#Base 16:   404a9d9b² == 1025648cfea37bd9

use Test::More;
chomp $ref;
is($result, $ref);
done_testing;

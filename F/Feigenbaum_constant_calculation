#!/usr/local/bin/perl
#u# http://rosettacode.org/wiki/Feigenbaum_constant_calculation
#c# 2018-10-17 >RC
#p# OK
#n# fails with native squaring, works with Math-GS, and even better with Math::AnyNum
#n# except on Mac OS: https://github.com/leto/math--gsl/issues/151
#n# cf: https://github.com/SqrtNegInf/Feigenbaum

use strict;
use warnings;
use feature 'say';
my $result;

use Math::AnyNum 'sqr';

my $a1 = 1.0;
my $a2 = 0.0;
my $d1 = 3.2;

print " i         d\n";

for my $i (2..13) {
    my $a = $a1 + ($a1 - $a2)/$d1;
    for (1..10) {
        my $x = 0; my $y = 0;
        for (1 .. 2**$i) {
            $y = 1 - 2 * $y * $x;
            #$x = $a - $x*$x;            # right
            #$x = $a - gsl_pow_2($x);   # right
            $x = $a - sqr($x);          # converges better
            #$x = $a - $x**2;           # potentially wrong
        }
        $a -= $x/$y;
     }

     $d1 = ($a1 - $a2)/($a - $a1);
     ($a2, $a1) = ($a1, $a);
     $result .= sprintf "%2d %17.14f\n", $i, $d1;
}

$result .= "δ = $d1\n";

say $result;

my $ref = <<'END';
 2  3.21851142203809
 3  4.38567759856834
 4  4.60094927653808
 5  4.65513049539198
 6  4.66611194782857
 7  4.66854858144684
 8  4.66906066064827
 9  4.66917155537951
10  4.66919515603002
11  4.66920022908686
12  4.66920131329420
13  4.66920154578091
δ = 4.66920154578090670750605810993042973643156433045
END

# using $x**2: δ = 4.66920315860471

use Test::More;
is($result, $ref);
done_testing;

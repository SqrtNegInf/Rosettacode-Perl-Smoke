#!/usr/local/bin/perl
#u# http://rosettacode.org/wiki/Find_the_intersection_of_a_line_with_a_plane
#c# 2018-09-27 >RC
#p# OK

use strict;
use warnings;
use feature 'say';

package  Line; sub new { my ($c, $a) = @_; my $s = { P0 => $a->{P0}, u => $a->{u} } } # point / ray
package Plane; sub new { my ($c, $a) = @_; my $s = { V0 => $a->{V0}, n => $a->{n} } } # point / normal

package main;

sub dot { my $p; $p    += $_[0][$_] * $_[1][$_] for 0..@{$_[0]}-1; $p } # dot product
sub vd  { my @v; $v[$_] = $_[0][$_] - $_[1][$_] for 0..@{$_[0]}-1; @v } # vector difference
sub va  { my @v; $v[$_] = $_[0][$_] + $_[1][$_] for 0..@{$_[0]}-1; @v } # vector addition
sub vp  { my @v; $v[$_] = $_[0][$_] * $_[1][$_] for 0..@{$_[0]}-1; @v } # vector product

sub line_plane_intersection {
    my($L, $P) = @_;

    my $cos = dot($L->{u}, $P->{n});     # cosine between normal & ray
    return 'Vectors are orthogonol; no intersection or line within plane' if $cos == 0;

    my @W = vd($L->{P0},$P->{V0});       # difference between P0 and V0
    my $SI = -dot($P->{n}, \@W) / $cos;  # line segment where it intersets the plane

    my @a = vp($L->{u},[($SI) x 3]);
    my @b = va($P->{V0},\@a);
    va(\@W,\@b);
}
 
say 'Intersection at point: ', join ' ', my @p = line_plane_intersection(
         Line->new({ P0=>[0,0,10], u=>[0,-1,-1]}),
        Plane->new({ V0=>[0,0,5 ], n=>[0, 0, 1]}) 
);

use Test::More;
is ((join ' ', @p), '0 -5 5');
done_testing;

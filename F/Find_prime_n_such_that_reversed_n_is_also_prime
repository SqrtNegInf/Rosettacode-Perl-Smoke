#!/usr/local/bin/perl
#u# http://rosettacode.org/wiki/Find_prime_n_for_that_reversed_n_is_also_prime
#c# 2021-03-22 >RC
#p# OK

use strict;
use warnings;
use feature 'say';

use ntheory 'is_prime';
use List::Util 'max';

sub pp {
    my $format = ('%' . (my $cw = 1+length max @_) . 'd') x @_;
    my $width  = ".{@{[$cw * int 60/$cw]}}"; # no diff vs 'qr'?
    (sprintf($format, @_)) =~ s/($width)/$1\n/gr;
}

my($limit, @rp) = 500;
is_prime($_) and is_prime(reverse $_) and push @rp, $_ for 1..$limit;
say my $result = @rp . " reversible primes < $limit:\n" . pp(@rp);

my $ref = <<'END';
34 reversible primes < 500:
   2   3   5   7  11  13  17  31  37  71  73  79  97 101 107
 113 131 149 151 157 167 179 181 191 199 311 313 337 347 353
 359 373 383 389
END

use Test::More;
chomp $ref;
is($result, $ref);
done_testing();

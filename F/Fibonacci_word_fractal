#!/usr/local/bin/perl
#u# http://rosettacode.org/wiki/Fibonacci_word/fractal
#c# 2018-08-03 <RC, 2023-03-31 >RC
#p# OK
#f# RC file: fword.png

# Creates file fword.png containing the Fibonacci Fractal.

use strict;
use warnings;
use GD;

my @fword = ( undef, 1, 0 );

sub fword {
	my $n = shift;
	return $fword[$n] if $n<3;
	return $fword[$n] //= fword($n-1).fword($n-2);
}

my $size = 3000;
my $im = GD::Image->new($size,$size);
my $white = $im->colorAllocate(255,255,255);
my $black = $im->colorAllocate(0,0,0);       
$im->transparent($white);
$im->interlaced('true');

my @pos   = (0,0);
my @dir   = (0,5);
my @steps = split //, fword 23;
my $i     = 1;
for( @steps ) {
	my @next = ( $pos[0]+$dir[0], $pos[1]+$dir[1] );
	$im->line( @pos, @next, $black );
	@dir = (  $dir[1], -$dir[0] ) if 0==$_ && 1==$i%2; # odd
	@dir = ( -$dir[1],  $dir[0] ) if 0==$_ && 0==$i%2; # even
	$i++;
	@pos = @next;
}

open my $out, ">", "run/fword.png" or die "Cannot open output file.\n";
binmode $out;
print $out $im->png;
close $out;

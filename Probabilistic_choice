#!/usr/local/bin/perl
#u# http://rosettacode.org/wiki/Probabilistic_choice
#c# 2018-08-20 <RC
#p# OK
#n# no testing, can't stop random?

srand 123456;

use feature 'say';
my $result;

use List::Util qw(first sum);
use constant TRIALS => 1e4;

sub prob_choice_picker {
  my %options = @_;
  my ($n, @a) = 0;
  while (my ($k,$v) = each %options) {
      $n += $v;
      push @a, [$n, $k];
  }
  return sub {
      my $r = rand;
      ( first {$r <= $_->[0]} @a )->[1];
  };
}

my %ps =
  (aleph  => 1/5,
   beth   => 1/6,
   gimel  => 1/7,
   daleth => 1/8,
   he     => 1/9,
   waw    => 1/10,
   zayin  => 1/11);
$ps{heth} = 1 - sum values %ps;

my $picker = prob_choice_picker %ps;
my %results;
for (my $n = 0 ; $n < TRIALS ; ++$n) {
    ++$results{$picker->()};
}

print "Event   Occurred  Expected  Difference\n";
foreach (sort {$results{$b} <=> $results{$a}} keys %results) {
    $result .= sprintf "%-6s  %f  %f  %f\n",
        $_, $results{$_}/TRIALS, $ps{$_},
        abs($results{$_}/TRIALS - $ps{$_});
}

say $result; 
my $ref = <<'EOD';
aleph   0.200136  0.200000  0.000136
beth    0.166393  0.166667  0.000274
gimel   0.142336  0.142857  0.000521
daleth  0.125876  0.125000  0.000876
he      0.110803  0.111111  0.000308
waw     0.100205  0.100000  0.000205
zayin   0.091095  0.090909  0.000186
heth    0.063156  0.063456  0.000300
EOD

use Test::More;
#is ($result, $ref);
#done_testing();

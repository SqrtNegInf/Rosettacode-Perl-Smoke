#!/usr/local/bin/perl
#u# http://rosettacode.org/wiki/Unprimeable_numbers
#c# 2019-12-06 >RC
#p# OK

use strict;
use warnings;
use feature 'say';

my @res;

use ntheory 'is_prime';
use enum qw(False True);

sub is_unprimeable {
    my($n) = @_;
    return False if is_prime($n);
    my $chrs = length $n;

    for my $place (0..$chrs-1) {
        my $pow = 10**($chrs - $place - 1);
        my $this = substr($n, $place, 1) * $pow;
        map { return False if $this != $_ and is_prime($n - $this + $_ * $pow) } 0..9;
     }
     True
}

sub comma { reverse ((reverse shift) =~ s/(.{3})/$1,/gr) =~ s/^,//r }

my($n, @ups);

do { push @ups, $n if is_unprimeable(++$n); } until @ups == 600;
push @res, "First 35 unprimeables:\n" . join ' ', @ups[0..34];
push @res, sprintf "\n600th unprimeable: %s\n", comma $ups[599];

map {
    my $x = $_;
    while ($x += 10) { last if is_unprimeable($x) }
    push @res, "First unprimeable that ends with $_: " . sprintf "%9s", comma $x;
} 0..9;

say my $result = join "\n", @res;

my $ref = <<'END';
First 35 unprimeables:
200 204 206 208 320 322 324 325 326 328 510 512 514 515 516 518 530 532 534 535 536 538 620 622 624 625 626 628 840 842 844 845 846 848 890

600th unprimeable: 5,242

First unprimeable that ends with 0:       200
First unprimeable that ends with 1:   595,631
First unprimeable that ends with 2:       322
First unprimeable that ends with 3: 1,203,623
First unprimeable that ends with 4:       204
First unprimeable that ends with 5:       325
First unprimeable that ends with 6:       206
First unprimeable that ends with 7:   872,897
First unprimeable that ends with 8:       208
First unprimeable that ends with 9:   212,159
END

use Test::More;
chomp $ref;
is($result, $ref);
done_testing;

#!/usr/local/bin/perl
#u# http://rosettacode.org/wiki/URL_parser
#c# 2018-08-07 <RC
#p# OK

use strict;
use warnings;
use feature 'say';
my $result;

# You can use the URI module from CPAN to parse URIs. Note that the output is a bit different: for example, you don't get the host from the foo:// scheme, as host is only valid for schemes that define it.

use URI;

for my $uri (do { no warnings 'qw';
                  qw( foo://example.com:8042/over/there?name=ferret#nose
                      urn:example:animal:ferret:nose
                      jdbc:mysql://test_user:ouupppssss@localhost:3306/sakila?profileSQL=true
                      ftp://ftp.is.co.za/rfc/rfc1808.txt
                      http://www.ietf.org/rfc/rfc2396.txt#header1
                      ldap://[2001:db8::7]/c=GB?objectClass=one&objectClass=two
                      mailto:John.Doe@example.com
                      news:comp.infosystems.www.servers.unix
                      tel:+1-816-555-1212
                      telnet://192.0.2.16:80/
                      urn:oasis:names:specification:docbook:dtd:xml:4.1.2
                   )}) {
    my $u = 'URI'->new($uri);
    $result .= "$uri\n";
    for my $part (<scheme path fragment authority host port query>) {
        eval { my $parsed = $u->$part;
               $result .= "\t". $part. "\t". $parsed. "\n" if defined $parsed;
        };
    }

}

say $result; 
my $ref = <<'EOD';
foo://example.com:8042/over/there?name=ferret#nose
	scheme	foo
	path	/over/there
	fragment	nose
	authority	example.com:8042
	query	name=ferret
urn:example:animal:ferret:nose
	scheme	urn
	path	example:animal:ferret:nose
jdbc:mysql://test_user:ouupppssss@localhost:3306/sakila?profileSQL=true
	scheme	jdbc
	path	mysql://test_user:ouupppssss@localhost:3306/sakila
	query	profileSQL=true
ftp://ftp.is.co.za/rfc/rfc1808.txt
	scheme	ftp
	path	/rfc/rfc1808.txt
	authority	ftp.is.co.za
	host	ftp.is.co.za
	port	21
http://www.ietf.org/rfc/rfc2396.txt#header1
	scheme	http
	path	/rfc/rfc2396.txt
	fragment	header1
	authority	www.ietf.org
	host	www.ietf.org
	port	80
ldap://[2001:db8::7]/c=GB?objectClass=one&objectClass=two
	scheme	ldap
	path	/c=GB
	authority	[2001:db8::7]
	host	2001:db8::7
	port	389
	query	objectClass=one&objectClass=two
mailto:John.Doe@example.com
	scheme	mailto
	path	John.Doe@example.com
news:comp.infosystems.www.servers.unix
	scheme	news
	path	comp.infosystems.www.servers.unix
	port	119
tel:+1-816-555-1212
	scheme	tel
	path	+1-816-555-1212
telnet://192.0.2.16:80/
	scheme	telnet
	path	/
	authority	192.0.2.16:80
	host	192.0.2.16
	port	80
urn:oasis:names:specification:docbook:dtd:xml:4.1.2
	scheme	urn
	path	oasis:names:specification:docbook:dtd:xml:4.1.2
EOD

use Test::More;
is ($result, $ref);
done_testing;

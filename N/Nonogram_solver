#!/usr/local/bin/perl
#u# http://rosettacode.org/wiki/Nonogram_solver
#c# 2019-04-02 <RC, 2019-05-16 >RC, 2020-01-25 >RC
#p# OK
#n# move external file to DATA handle

use strict;
use warnings;
use feature 'say';
my @res;

while(my $row = <DATA>) {
  $row =~ /\S/ or next;
  my $column = <DATA>;
  my @rpats = makepatterns($row);
  my @cpats = makepatterns($column);
  my @rows = ( '.' x @cpats ) x @rpats;
  my $prev = '';
  while ($prev ne "@rows") {
    $prev = "@rows";
    try(\@rows, \@rpats);
    my @cols = map { join '', map { s/.//; $& } @rows } 0..$#cpats;
    try(\@cols, \@cpats);
    @rows = map { join '', map { s/.//; $& } @cols } 0..$#rpats;
  }
  push @res, "\n";
  push @res, "@rows" =~ /\./ ? "Failed\n" : map { tr/01/.#/r, "\n" } @rows;
}

sub try {
    my ($lines, $patterns) = @_;
    for my $i ( 0 .. $#$lines ) {
        while( $lines->[$i] =~ /\./g ) {
            for my $try ( 0, 1 ) {
                next if ($lines->[$i] =~ s/.\G/$try/r) =~ $patterns->[$i];
                $lines->[$i] =~ s// 1 - $try /e; # substitute where \G left the cursor
            }
        }
    }
}

sub makepatterns {
    map { qr/^$_$/                          # 7  convert strings to regex
        } map {  '[0.]*'                    # 6a prepend static pattern
               . join('[0.]+',              # 5  interleave with static pattern
                       map { "[1.]{$_}"     # 4  require to match exactly 'n' times
                           } map { -64+ord  # 3  convert letter value to repetition count 'n'
                                 } split // # 2  for each letter in group
                     )
               . '[0.]*'                    # 6b append static pattern
              } split ' ', shift;           # 1  do all of the above for each letter grouping
}

my $result = join '', @res;

my $ref = <<'EOD';

.###....
##.#....
.###..##
..##..##
..######
#.#####.
######..
....#...
...##...

..........######....
........###.#..###..
...#..###...#....###
..###.##############
...#..#............#
..#.#.##..........##
#####..##........##.
#####...#........#..
#####..###.###.###..
########.###.###.###

....###.#...........
....##.####.#.......
....#.###.###.......
..##.####...........
.###.###.#....###...
###..##.##...#.###..
##..##.##....##.##..
....##.#.#..##.#.#..
....#.##.#...####...
....#.#.##.....##...
.....##.##..########
....##.##...##..####
....#.##.##.#...#..#
###..###.#####.....#
#.#.###.#....#....##
##..###.#....###.###
.#.###.##.########..
.####.###.########..
...#.####.##.#####..
...#.####.##...##...
....####..##...#####
...#####.###...#####
...####.#..........#
..####.##...........
..###.###...........

....................#####
..##..............###..##
.##..............#####..#
##.............########..
##....#####.###########..
#.#..##....#....######...
#..##.....#.......###....
##........#.............#
.##.....######.........##
..###############....####
.....##########..########
....##.#.####.###..######
........#################
........#################
.......##################
.......#...##############
.......#.#.##############
........#####...#########
.................########
..................#######
EOD

use Test::More;
is ($result, $ref);
done_testing();

__DATA__
C BA CB BB F AE F A B
AB CA AE GA E C D C

F CAC ACAC CN AAA AABB EBB EAA ECCC HCCC
D D AE CD AE A DA BBB CC AAB BAA AAB DA AAB AAA BAB AAA CD BBA DA

CA BDA ACC BD CCAC CBBAC BBBBB BAABAA ABAD AABB BBH BBBD ABBAAA CCEA AACAAB BCACC ACBH DCH ADBE ADBB DBE ECE DAA DB CC
BC CAC CBAB BDD CDBDE BEBDF ADCDFA DCCFB DBCFC ABDBA BBF AAF BADB DBF AAAAD BDG CEF CBDB BBB FC

E BCB BEA BH BEK AABAF ABAC BAA BFB OD JH BADCF Q Q R AN AAN EI H G
E CB BAB AAA AAA AC BB ACC ACCA AGB AIA AJ AJ ACE AH BAF CAG DAG FAH FJ GJ ADK ABK BL CM

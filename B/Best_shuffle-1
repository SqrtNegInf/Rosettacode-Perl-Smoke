#!/usr/local/bin/perl
#u# http://rosettacode.org/wiki/Best_shuffle
#c# 2018-07-16 <RC, 2023-04-03 >RC
#p# OK
#n# we get rid of the module, see -2 version

my $result;

srand 123456;

use strict;
use warnings;
use feature 'say';

use feature 'bitwise';

use List::Util 'shuffle';
use Algorithm::Permute;

best_shuffle($_) for <abracadabra seesaw elk grrrrrr up a>;

sub best_shuffle {
	my ($original_word) = @_;
	my $best_word = $original_word;
	my $best_score = length $best_word;

	my @shuffled = shuffle split //, $original_word;
	my $iterator = Algorithm::Permute->new(\@shuffled);
	
	while( my @array = $iterator->next ) {
		my $word = join '', @array;
		# For each letter which is the same in the two words,
		# there will be a \x00 in the "^" of the two words.
		# The tr operator is then used to count the "\x00"s.
		my $score = ($original_word ^. $word) =~ tr/\x00//;
		next if $score >= $best_score;
		($best_word, $best_score) = ($word, $score);
		last if $score == 0;
	}
	
	$result .= sprintf "$original_word, $best_word, $best_score\n";
}

say $result;
my $ref = <<'END';
abracadabra, baadabacrar, 0
seesaw, wasees, 0
elk, lke, 0
grrrrrr, rrrrrrg, 5
up, pu, 0
a, a, 1
END

use Test::More;
is ($result, $ref);
done_testing;

#!/usr/local/bin/perl
#u# http://rosettacode.org/wiki/Bitwise_IO
#c# 2018-07-17 <RC
#p# OK
#f# RC file: bitwise.step2
#n# always worked, now smoke-tested

use strict;
#use warnings; # line 68: $buf < 0
 
# $buffer = write_bits(*STDOUT, $buffer, $number, $bits)
sub write_bits( $$$$ ) {
    my ($out, $l, $num, $q) = @_;
    $l .= substr(unpack("B*", pack("N", $num)),
		 -$q);
    if ( (length($l) > 8) ) {
	my $left = substr($l, 8);
	print $out pack("B8", $l);
	$l = $left;
    }
    return $l;
}
 
# flush_bits(*STDOUT, $buffer)
sub flush_bits( $$ ) {
    my ($out, $b) = @_;
    print $out pack("B*", $b);
}
 
# ($val, $buf) = read_bits(*STDIN, $buf, $n)
sub read_bits( $$$ ) {
    my ( $in, $b, $n ) = @_;
    # we put a limit in the number of bits we can read
    # with one shot; this should mirror the limit of the max
    # integer value perl can hold
    if ( $n > 32 ) { return 0; }
    while ( length($b) < $n ) {
	my $v;
	my $red = read($in, $v, 1);
	if ( $red < 1 ) { return ( 0, -1 ); }
	$b .= substr(unpack("B*", $v), -8);
    }
    my $bits = "0" x ( 32-$n ) . substr($b, 0, $n);
    my $val = unpack("N", pack("B32", $bits));
    $b = substr($b, $n);
    return ($val, $b);
}

# Crunching bytes discarding most significant bit (lossless compression for ASCII and few more!)
my $buf = "";
my $c;
open my $f_in,  '<', 'ref/passwd.txt';
open my $f_step1, '>', 'run/bitwise.step1';
while( read($f_in, $c, 1) > 0 ) {
    $buf = write_bits($f_step1, $buf, unpack("C1", $c), 7);
}
flush_bits($f_step1, $buf);
close $f_step1;
# Expanding each seven bits to fit a byte (padding the eight most significant bit with 0):

# ($val, $buf) = read_bits(*STDIN, $buf, $n)
open    $f_step1, '<', 'run/bitwise.step1';
open my $f_step2, '>', 'run/bitwise.step2';
$buf = "";
my $v;
while(1) {
    ( $v, $buf ) = read_bits($f_step1, $buf, 7);
    last if ($buf < 0); 
    print $f_step2 pack("C1", $v);
}

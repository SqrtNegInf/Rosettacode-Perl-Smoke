#!/usr/local/bin/perl
#u# https://www.rosettacode.org/wiki/Blum_integer
#c# 2023-12-10 <RC
#p# OK
#n# Trizen, faster than original

use v5.36;

my @res;

use ntheory qw(is_square is_semiprime factor vecall);

sub comma          { reverse((reverse shift) =~ s/.{3}\K/,/gr)                      =~ s/^,//r }
sub table ($c, @V) { my $t = $c * (my $w = 5); (sprintf(('%' . $w . 'd') x @V, @V)) =~ s/.{1,$t}\K/\n/gr }

sub is_blum ($n) {
    ($n % 4) == 1 && is_semiprime($n) && !is_square($n) && vecall { ($_ % 4) == 3 } factor($n);
}

my @nth = (26828, 1e5, 2e5, 3e5, 4e5);

my (@blum, %C);
for (my $i = 1 ; ; ++$i) {
    push @blum, $i if is_blum $i;
    last if $nth[-1] == @blum;
}
$C{$_ % 10}++ for @blum;

push @res, "The first fifty Blum integers:\n" . table 10, @blum[0 .. 49];
push @res, sprintf "The %7sth Blum integer: %9s", comma($_), comma $blum[$_ - 1] for @nth;
push @res, '';
push @res, sprintf "$_: %6d (%.3f%%)", $C{$_}, 100 * $C{$_} / scalar @blum for <1 3 7 9>;

my $ref = <<'EOD';
The first fifty Blum integers:
   21   33   57   69   77   93  129  133  141  161
  177  201  209  213  217  237  249  253  301  309
  321  329  341  381  393  413  417  437  453  469
  473  489  497  501  517  537  553  573  581  589
  597  633  649  669  681  713  717  721  737  749

The  26,828th Blum integer:   524,273
The 100,000th Blum integer: 2,075,217
The 200,000th Blum integer: 4,275,533
The 300,000th Blum integer: 6,521,629
The 400,000th Blum integer: 8,802,377

1: 100005 (25.001%)
3: 100067 (25.017%)
7:  99989 (24.997%)
9:  99939 (24.985%)
EOD

say my $result = join "\n", @res;

use Test::More;
chomp $ref;
is ($result, $ref);
done_testing();

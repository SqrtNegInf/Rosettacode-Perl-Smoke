#!/usr/local/bin/perl
#u# http://rosettacode.org/wiki/Bitmap/Flood_fill
#c# 2018-07-18 <RC
#p# OK
#f# RC file: filledcircle2.jpg

use strict;
use warnings;

use Image::Imlib2;
 
my $image1 = Image::Imlib2->load("ref/unfilledcircle.jpg");
$image1->set_color(0, 255, 0, 255);
$image1->fill(100,100);
$image1->save("run/filledcircle1.jpg");

sub colordistance {
    my ( $c1, $c2 ) = @_;
 
    my ( $r1, $g1, $b1 ) = @$c1;
    my ( $r2, $g2, $b2 ) = @$c2;
    return sqrt(( ($r1-$r2)**2 + ($g1-$g2)**2 + ($b1-$b2)**2 ))/(255.0*sqrt(3.0));
}
 
sub floodfill {
    my ( $image, $x, $y, $r, $g, $b ) = @_;
    my $distparameter = 0.2;
    my (%visited,@queue);
 
    my @tcol = ( $r, $g, $b );
    my @col = $image->query_pixel($x, $y);
    if ( colordistance(\@tcol, \@col) > $distparameter ) { return; }
    push @queue, [$x, $y];
    while ( @queue ) {
        my $pointref = shift(@queue);
        ( $x, $y ) = @$pointref;
        if ( ($x < 0) || ($y < 0) || ( $x >= $image->width ) || ( $y >= $image->height ) ) { next; }
        if ( ! exists($visited{"$x,$y"}) ) {
            @col = $image->query_pixel($x, $y);
            if ( colordistance(\@tcol, \@col) <= $distparameter ) {
                $image->draw_point($x, $y);
                $visited{"$x,$y"} = 1;
                push @queue, [$x+1, $y],
                             [$x-1, $y],
                             [$x, $y+1],
                             [$x, $y-1];
            }
        }
    }
}
 
my $image2 = Image::Imlib2->load("ref/unfilledcircle.jpg");
$image2->set_color(0,255,0,255);
floodfill($image2, 100,100, 0, 0, 0);
$image2->save("run/filledcircle2.jpg");

# This fills better than the Image::Imlib2 fill function the inner circle, since because of JPG compression and thanks to the $distparameter, it "sees" as black also pixel that are no more exactly black.

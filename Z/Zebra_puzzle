#!/usr/local/bin/perl
#u# http://rosettacode.org/wiki/Zebra_puzzle
#c# 2018-07-11 <RC
#p# OK
#n# minor changes not on RC

use strict;
use warnings;
use feature 'say';
my $result;

# Basically the same idea as C, though of course it's much easier to have Perl generate Perl code.

#use utf8;
#binmode STDOUT, ":utf8";

my (@tgt, %names);
sub setprops {
	my %h = @_;
	my @p = keys %h;
	for my $p (@p) {
		my @v = @{ $h{$p} };
		@tgt = map(+{idx=>$_-1, map{ ($_, undef) } @p}, 1 .. @v)
			unless @tgt;
		$names{$_} = $p for @v;
	}
}

my $solve = sub {
	for my $i (@tgt) {
		#$result .= sprintf("%12s", ucfirst($i->{$_} // "¿Qué?"))
		$result .= sprintf("%12s", ucfirst($i->{$_} // 'Zebra')) # DH - answer the initial question
				for reverse sort keys %$i;
		$result .= "\n";
	}
	#"there is only one"  # <--- change this to a false value to find all solutions (if any)
};

sub pair {
	my ($a, $b, @v) = @_;
	if ($a =~ /^(\d+)$/) {
		$tgt[$1]{ $names{$b} } = $b;
		return;
	}

	@v = (0) unless @v;
	my %allowed;
	$allowed{$_} = 1 for @v;

	my ($p1, $p2) = ($names{$a}, $names{$b});

	my $e = $solve;
	$solve = sub {		# <--- sorta like how TeX \let...\def macro
		my ($x, $y);

		($x) = grep { $_->{$p1} && $_->{$p1} eq $a } @tgt;
		($y) = grep { $_->{$p2} && $_->{$p2} eq $b } @tgt;

		$x and $y and
			return $allowed{ $x->{idx} - $y->{idx} } && $e->();

		my $try_stuff = sub {
			my ($this, $p, $v, $sign) = @_;
			for (@v) {
				my $i = $this->{idx} + $sign * $_;
				next unless $i >= 0 && $i < @tgt && !$tgt[$i]{$p};
				local $tgt[$i]{$p} = $v;
				$e->() and return 1;
			}
			return
		};

		$x and return $try_stuff->($x, $p2, $b, 1);
		$y and return $try_stuff->($y, $p1, $a, -1);

		for $x (@tgt) {
			next if $x->{$p1};
			local $x->{$p1} = $a;
			$try_stuff->($x, $p2, $b, 1) and return 1;
		}
	};
}

# ---- above should be generic for all similar puzzles ---- #

# ---- below: per puzzle setup ---- #
# property names and values
setprops (
	# Svensk n. a Swede, not a swede (kålrot).
	# AEnglisk (from middle Viking "Æŋløsåksen") n. a Brit.
	'Who'	=> [ <Deutsch Svensk Norske Danske AEnglisk> ],
	'Pet'	=> [ <birds dog horse zebra cats> ],
	'Drink'	=> [ <water tea milk beer coffee> ],
	'Smoke'	=> [ <dunhill blue_master prince blend pall_mall> ],
	'Color'	=> [ <red green yellow white blue> ]
);

# constraints
pair <AEnglisk red >;
pair <Svensk dog >;
pair <Danske tea >;
pair <green white 1 >;	# "to the left of" can mean either 1 or -1: ambiguous
pair <coffee green >;
pair <pall_mall birds >;
pair <yellow dunhill >;
pair <2 milk >;
pair <0 Norske >;
pair <blend cats -1 1 >;
pair <horse dunhill -1 1 >;
pair <blue_master beer >;	# Nicht das Deutsche Bier trinken? Huh.
pair <Deutsch prince >;
pair <Norske blue -1 1 >;
pair <water blend -1 1 >;

$solve->();
say $result;

#Incidentally, the same logic can be used to solve the dwelling problem, if somewhat awkwardly:
#...
# property names and values
#setprops
#	'Who'	=> [ qw(baker cooper fletcher miller smith) ],
#	'Level'	=> [ qw(one two three four five) ];
# 
## constraints
#pair qw(0 one);
#pair qw(1 two);
#pair qw(2 three);
#pair qw(3 four);
#pair qw(4 five);
#pair qw(baker five -4 -3 -2 -1 1 2 3 4);
#pair qw(cooper one -4 -3 -2 -1 1 2 3 4);
#pair qw(fletcher one -4 -3 -2 -1 1 2 3 4);
#pair qw(fletcher five -4 -3 -2 -1 1 2 3 4);
#pair qw(miller cooper -1 -2 -3 -4);
#pair qw(smith fletcher 4 3 2 -2 -3 -4);
#pair qw(cooper fletcher 4 3 2 -2 -3 -4);
# 
#$solve->();

my $ref = <<EOD;
           0      Norske     Dunhill        Cats       Water      Yellow
           1      Danske       Blend       Horse         Tea        Blue
           2    AEnglisk   Pall_mall       Birds        Milk         Red
           3     Deutsch      Prince       Zebra      Coffee       Green
           4      Svensk Blue_master         Dog        Beer       White
EOD

use Test::More;
is ($result, $ref);
done_testing;

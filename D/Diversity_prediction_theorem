#!/usr/local/bin/perl
#u# http://rosettacode.org/wiki/Diversity_prediction_theorem
#c# 2018-09-28 >RC
#p# OK

use feature 'say';

sub diversity_calc {
    my($truth, @pred) = @_;
    my($ae,$ce,$cp,$pd,$stats);

    $cp += $_/@pred for @pred;      # collective prediction
    $ae = avg_error($truth, @pred); # average individual error
    $ce = ($cp - $truth)**2;        # collective error
    $pd = avg_error($cp, @pred);    # prediction diversity
    
    my $fmt = "%13s: %6.3f\n";
    $stats  = sprintf $fmt, 'average-error', $ae;
    $stats .= sprintf $fmt, 'crowd-error',   $ce;
    $stats .= sprintf $fmt, 'diversity',     $pd;
}

sub avg_error {
    my($m, @v) = @_;
    my($avg_err);
    $avg_err += ($_ - $m)**2 for @v;
    $avg_err/@v;
}

$result .= diversity_calc(49, qw<48 47 51>);
$result .= diversity_calc(49, qw<48 47 51 42>);

say $result;

my $ref = <<'END';
average-error:  3.000
  crowd-error:  0.111
    diversity:  2.889
average-error: 14.500
  crowd-error:  4.000
    diversity: 10.500
END

use Test::More;
is ($result, $ref);
done_testing();

#!/usr/local/bin/perl
#u# http://rosettacode.org/wiki/Determine_if_a_string_has_all_the_same_characters
#c# 2019-11-03 >RC, 2019-11-07 >RC
#p# OK

use strict;
use warnings;
use feature 'say';
use utf8;
binmode(STDOUT, ':utf8');
use List::AllUtils qw(uniq);
use Unicode::Normalize qw(NFC);
use Unicode::UCD 'charinfo';

my @res;

for my $str (
    '',
    '   ',
    '2',
    '333',
    '.55',
    'tttTTT',
    '4444 444k',
    'Î”ðŸ‘ðŸ‘¨',
    'ðŸ‡¬ðŸ‡§ðŸ‡¬ðŸ‡§ðŸ‡¬ðŸ‡§ðŸ‡¬ðŸ‡§',
    "\N{LATIN CAPITAL LETTER A}\N{COMBINING DIAERESIS}\N{COMBINING MACRON}" .
    "\N{LATIN CAPITAL LETTER A WITH DIAERESIS}\N{COMBINING MACRON}" .
    "\N{LATIN CAPITAL LETTER A WITH DIAERESIS AND MACRON}"
) {
    my @S;
    push @S, NFC $1 while $str =~ /(\X)/g;
    push @res, sprintf qq{\n"$str" (length: %d) has }, scalar @S;
    my @U = uniq @S;
    if (1 != @U and @U > 0) {
        $res[-1] .= 'different characters:';
        for my $l (@U) {
            push @res, sprintf "'%s' %s (0x%x) in positions: %s", 
                $l, charinfo(ord $l)->{'name'}, ord($l), join ', ', map { 1+$_ } grep { $l eq $S[$_] } 0..$#S;
        }
    } else {
        $res[-1] .= 'the same character in all positions.'
    }
}

say my $result = join "\n", @res;

my $ref = <<'END';

"" (length: 0) has the same character in all positions.

"   " (length: 3) has the same character in all positions.

"2" (length: 1) has the same character in all positions.

"333" (length: 3) has the same character in all positions.

".55" (length: 3) has different characters:
'.' FULL STOP (0x2e) in positions: 1
'5' DIGIT FIVE (0x35) in positions: 2, 3

"tttTTT" (length: 6) has different characters:
't' LATIN SMALL LETTER T (0x74) in positions: 1, 2, 3
'T' LATIN CAPITAL LETTER T (0x54) in positions: 4, 5, 6

"4444 444k" (length: 9) has different characters:
'4' DIGIT FOUR (0x34) in positions: 1, 2, 3, 4, 6, 7, 8
' ' SPACE (0x20) in positions: 5
'k' LATIN SMALL LETTER K (0x6b) in positions: 9

"Î”ðŸ‘ðŸ‘¨" (length: 3) has different characters:
'Î”' GREEK CAPITAL LETTER DELTA (0x394) in positions: 1
'ðŸ‘' THUMBS UP SIGN (0x1f44d) in positions: 2
'ðŸ‘¨' MAN (0x1f468) in positions: 3

"ðŸ‡¬ðŸ‡§ðŸ‡¬ðŸ‡§ðŸ‡¬ðŸ‡§ðŸ‡¬ðŸ‡§" (length: 4) has the same character in all positions.

"AÌˆÌ„Ã„Ì„Çž" (length: 3) has the same character in all positions.
END


use Test::More;
chomp $ref;
is($result, $ref);
done_testing;

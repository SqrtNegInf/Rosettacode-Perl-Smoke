#!/usr/local/bin/perl
#u# http://rosettacode.org/wiki/De_Bruijn_sequences
#c# 2019-09-03 >RC
#p# OK

use strict;
use warnings;
use feature 'say';

my @res;

# Deviates very slightly from the task spec. (Replaces the 4444th character with a 5 rather than a '.'), mostly so it can demonstrate detection of extra PINs as well as missing ones.

my $seq;
for my $x (0..99) {
    my $a = sprintf '%02d', $x;
    next if substr($a,1,1) < substr($a,0,1);
    $seq .= (substr($a,0,1) == substr($a,1,1)) ? substr($a,0,1) : $a;
    for ($a+1 .. 99) {
        next if substr(sprintf('%02d', $_), 1,1) <= substr($a,0,1);
        $seq .= sprintf "%s%02d", $a, $_;
    }
}
$seq .= '000';

sub check {
    my($seq) = @_;
    my %chk;
    for (0.. -1 + length $seq) { $chk{substr($seq, $_, 4)}++ }
    push @res, 'Missing: ' . join ' ', grep { ! $chk{ sprintf('%04d',$_) } } 0..9999;
    push @res, 'Extra:   ' . join ' ', sort grep { $chk{$_} > 1 } keys %chk;
}

my $n = 130;
push @res, "de Bruijn sequence length: " . length $seq;
push @res, "\nFirst $n characters:\n" . substr($seq, 0, $n );
push @res, "\nLast $n characters:\n"  . substr($seq, -$n, $n);
push @res, "\nIncorrect 4 digit PINs in this sequence:";
check $seq;

push @res, "\nIncorrect 4 digit PINs in the reversed sequence:";
check(reverse $seq);

push @res, "\nReplacing the 4444th digit, '@{[substr($seq,4443,1)]}', with '5'";
substr $seq, 4443, 1, 5;
push @res, "Incorrect 4 digit PINs in the revised sequence:";
check $seq;

say my $result = join "\n", @res;

my $ref = <<"EOD";
de Bruijn sequence length: 10003

First $n characters:
0000100020003000400050006000700080009001100120013001400150016001700180019002100220023002400250026002700280029003100320033003400350

Last $n characters:
6898689969697769786979698769886989699769986999777787779778877897798779978787978887889789878997979887989799879998888988998989999000

Incorrect 4 digit PINs in this sequence:
Missing: 
Extra:   

Incorrect 4 digit PINs in the reversed sequence:
Missing: 
Extra:   

Replacing the 4444th digit, '4', with '5'
Incorrect 4 digit PINs in the revised sequence:
Missing: 1459 4591 5814 8145
Extra:   1559 5591 5815 8155
EOD

use Test::More;
chomp $ref;
is($result, $ref);
done_testing;

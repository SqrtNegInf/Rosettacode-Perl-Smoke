#!/usr/local/bin/perl
#u# http://rosettacode.org/wiki/Determine_if_a_string_has_all_unique_characters
#c# 2019-11-04 >RC, 2019-11-07 >RC
#p# OK
#n# but the case of duplicated combining-chars still not handled, e.g. 'Î”Î´Î”Ì‚Î”Î›Î”Ì‚' should be:  [Raku gets it right of course...]
#n# 'Î”Ì‚' GREEK CAPITAL LETTER DELTA COMBINING CIRCUMFLEX ACCENT (0x394 0x302) in positions: 3, 6

use strict;
use warnings;
use feature 'say';

use utf8;
binmode(STDOUT, ':utf8');
use List::AllUtils qw(uniq);
use Unicode::UCD 'charinfo';

my @res;

for my $str (
    '',
    '.',
    'abcABC',
    'XYZ ZYX',
    '1234567890ABCDEFGHIJKLMN0PQRSTUVWXYZ',
    '01234567890ABCDEFGHIJKLMN0PQRSTUVWXYZ0X',
    'Î”ğŸ‘ğŸ‘¨ğŸ‘Î”',
    'Î”Î´Î”Ì‚Î”Î›',
) {

    my @S;
    #my @S = split /\X/, $str; # why not?
    push @S, $1 while $str =~ /(\X)/g;
    push @res, sprintf qq{\n"$str" (length: %d) has }, scalar @S;

    if (@S != uniq @S) {
        $res[-1] .= 'duplicated characters:';
        my %P;
        push @{ $P{$S[$_]} }, 1+$_ for 0..$#S;
        for my $k (sort keys %P) {
            next unless @{$P{$k}} > 1;
            push @res, sprintf "'%s' %s (0x%x) in positions: %s",
                $k, charinfo(ord $k)->{'name'}, ord($k), join ', ', @{$P{$k}};
        }

    } else {
        $res[-1] .= 'no duplicated characters.'
    }
}

say my $result = join "\n", @res;

my $ref = <<'END';

"" (length: 0) has no duplicated characters.

"." (length: 1) has no duplicated characters.

"abcABC" (length: 6) has no duplicated characters.

"XYZ ZYX" (length: 7) has duplicated characters:
'X' LATIN CAPITAL LETTER X (0x58) in positions: 1, 7
'Y' LATIN CAPITAL LETTER Y (0x59) in positions: 2, 6
'Z' LATIN CAPITAL LETTER Z (0x5a) in positions: 3, 5

"1234567890ABCDEFGHIJKLMN0PQRSTUVWXYZ" (length: 36) has duplicated characters:
'0' DIGIT ZERO (0x30) in positions: 10, 25

"01234567890ABCDEFGHIJKLMN0PQRSTUVWXYZ0X" (length: 39) has duplicated characters:
'0' DIGIT ZERO (0x30) in positions: 1, 11, 26, 38
'X' LATIN CAPITAL LETTER X (0x58) in positions: 35, 39

"Î”ğŸ‘ğŸ‘¨ğŸ‘Î”" (length: 5) has duplicated characters:
'Î”' GREEK CAPITAL LETTER DELTA (0x394) in positions: 1, 5
'ğŸ‘' THUMBS UP SIGN (0x1f44d) in positions: 2, 4

"Î”Î´Î”Ì‚Î”Î›" (length: 5) has duplicated characters:
'Î”' GREEK CAPITAL LETTER DELTA (0x394) in positions: 1, 4
END

use Test::More;
chomp $ref;
is($result, $ref);
done_testing;

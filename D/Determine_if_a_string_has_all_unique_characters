#!/usr/local/bin/perl
#u# http://rosettacode.org/wiki/Determine_if_a_string_has_all_unique_characters
#c# 2019-11-04 >RC
#p# OK
#n# still not working for Unicode strings with 'joiners'

use strict;
use warnings;
use feature 'say';

use utf8;
binmode(STDOUT, ':utf8');
use List::AllUtils qw(uniq);
use Unicode::Normalize qw(NFC);
use Unicode::UCD 'charinfo';

my @res;

for my $str (
    '',
    '.',
    'abcABC',
    'XYZ ZYX',
    '1234567890ABCDEFGHIJKLMN0PQRSTUVWXYZ',
    '01234567890ABCDEFGHIJKLMN0PQRSTUVWXYZ0X',
    'Î”ğŸ‘ğŸ‘¨ğŸ‘Î”',
#    'ğŸ¦‹ğŸ™‚ğŸ‘¨â€ğŸ‘©â€ğŸ‘§â€ğŸ‘¦ğŸ™„Î”Î”Ì‚ ğŸ¦‹Î”ğŸ‘ğŸ‘¨â€ğŸ‘©â€ğŸ‘§â€ğŸ‘¦'
) {
#   $len++ while $str =~ /\X/g;
    push @res, sprintf qq{\n"$str" (length: %d) has }, my $len = length NFC $str;

    if ($len != uniq my @S = split //, NFC $str) {
        $res[-1] .= "duplicated characters:";
        my %P;
        push @{ $P{$S[$_]} }, 1+$_ for 0..$#S;
        for my $k (sort keys %P) {
            next unless @{$P{$k}} > 1;
            push @res, sprintf "'%s' %s (0x%x) in positions: %s",
                $k, charinfo(ord $k)->{'name'}, ord($k), join ', ', @{$P{$k}};
        }

    } else {
        $res[-1] .= "no duplicated characters."
    }
}

say my $result = join "\n", @res;

my $ref = <<'END';

"" (length: 0) has no duplicated characters.

"." (length: 1) has no duplicated characters.

"abcABC" (length: 6) has no duplicated characters.

"XYZ ZYX" (length: 7) has duplicated characters:
'X' LATIN CAPITAL LETTER X (0x58) in positions: 1, 7
'Y' LATIN CAPITAL LETTER Y (0x59) in positions: 2, 6
'Z' LATIN CAPITAL LETTER Z (0x5a) in positions: 3, 5

"1234567890ABCDEFGHIJKLMN0PQRSTUVWXYZ" (length: 36) has duplicated characters:
'0' DIGIT ZERO (0x30) in positions: 10, 25

"01234567890ABCDEFGHIJKLMN0PQRSTUVWXYZ0X" (length: 39) has duplicated characters:
'0' DIGIT ZERO (0x30) in positions: 1, 11, 26, 38
'X' LATIN CAPITAL LETTER X (0x58) in positions: 35, 39

"Î”ğŸ‘ğŸ‘¨ğŸ‘Î”" (length: 5) has duplicated characters:
'Î”' GREEK CAPITAL LETTER DELTA (0x394) in positions: 1, 5
'ğŸ‘' THUMBS UP SIGN (0x1f44d) in positions: 2, 4
END

# "ğŸ¦‹ğŸ™‚ğŸ‘¨â€ğŸ‘©â€ğŸ‘§â€ğŸ‘¦ğŸ™„Î”Î”Ì‚ ğŸ¦‹Î”ğŸ‘ğŸ‘¨â€ğŸ‘©â€ğŸ‘§â€ğŸ‘¦" (length: 11), has duplicated characters:
# 'ğŸ¦‹' (BUTTERFLY; hex ordinal: 0x1F98B) in positions: 1, 8
# 'ğŸ‘¨â€ğŸ‘©â€ğŸ‘§â€ğŸ‘¦' (MAN ZERO WIDTH JOINER WOMAN ZERO WIDTH JOINER GIRL ZERO WIDTH JOINER BOY; hex ordinal: 0x1F468 0x200D 0x1F469 0x200D 0x1F467 0x200D 0x1F466) in positions: 3, 11
# 'Î”' (GREEK CAPITAL LETTER DELTA; hex ordinal: 0x394) in positions: 5, 9

use Test::More;
chomp $ref;
is($result, $ref);
done_testing();

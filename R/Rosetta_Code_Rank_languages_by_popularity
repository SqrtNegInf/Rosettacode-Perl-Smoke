#!/usr/local/bin/perl
#u# http://rosettacode.org/wiki/Rosetta_Code/Rank_languages_by_popularity
#c# 2018-08-21 <RC, 2019-09-11 >RC, 2022-09-07 >RC
#p# OK
#n# test needs to be revised every time a new language reaches 1200 tasks
#n# 2020-01-xx Started out with: Phix, Julia, Go, Python, Raku, Perl
#n# 2020-01-29 --> 2023-03-16 'C' ... 'Jq' reach 1000
#n# 2023-03-26 Making the club more exclusive again, 1200 tasks is the cutoff (neatly truncating the list to 10 languages)
#n# 2023-06-05  now it's 11 (Ruby)

my @res;

use strict;
use warnings;
use feature 'say';

use MediaWiki::API;

my $api =
  MediaWiki::API->new( { api_url => 'https://rosettacode.org/w/api.php' } );

my @languages;
my $gcmcontinue;
while (1) {
    my $apih = $api->api(
        {
            action      => 'query',
            generator   => 'categorymembers',
            gcmtitle    => 'Category:Programming Languages',
            gcmlimit    => 350,
            prop        => 'categoryinfo',
            gcmcontinue => $gcmcontinue
        }
    );
    push @languages, values %{ $apih->{'query'}{'pages'} };

    last if not $gcmcontinue = $apih->{'continue'}{'gcmcontinue'};
}

for (@languages) {
    $_->{'title'} =~ s/Category://;
    $_->{'categoryinfo'}{'size'} //= 0;
}

my @sorted_languages =
  reverse sort { $a->{'categoryinfo'}{'size'} <=> $b->{'categoryinfo'}{'size'} }
  @languages;

binmode STDOUT, ':encoding(utf8)';
my $n = 1;
for (@sorted_languages) {
    next unless $_->{'categoryinfo'}{'size'} > 1200;
    push @res, sprintf "%3d. %20s - %3d", $n++, $_->{'title'},
      $_->{'categoryinfo'}{'size'};
}

say join "\n", @res;

use Test::More;
ok @res == 12;
done_testing();

#!/usr/local/bin/perl
#u# http://rosettacode.org/wiki/Rosetta_Code/Rank_languages_by_popularity
#c# 2018-08-21 <RC, 2019-09-11 >RC
#p# OK
#n# testing somewhat fragile
#n# 2020-11-05 Pleased to welcome 'J' to the '1000 club'
#n# 2020-01-29 Pleased to welcome 'C' to the '1000 club'
#n# 2020-02-24 'Java' joins the no-longer-quite-so-exclusive club

use 5.010;
use MediaWiki::API;

my $api =
  MediaWiki::API->new( { api_url => 'http://rosettacode.org/mw/api.php' } );

my @languages;
my $gcmcontinue;
while (1) {
    my $apih = $api->api(
        {
            action      => 'query',
            generator   => 'categorymembers',
            gcmtitle    => 'Category:Programming Languages',
            gcmlimit    => 350,
            prop        => 'categoryinfo',
            gcmcontinue => $gcmcontinue
        }
    );
    push @languages, values %{ $apih->{'query'}{'pages'} };

    last if not $gcmcontinue = $apih->{'continue'}{'gcmcontinue'};
}

for (@languages) {
    $_->{'title'} =~ s/Category://;
    $_->{'categoryinfo'}{'size'} //= 0;
}

my @sorted_languages =
  reverse sort { $a->{'categoryinfo'}{'size'} <=> $b->{'categoryinfo'}{'size'} }
  @languages;

binmode STDOUT, ':encoding(utf8)';
my $n = 1;
for (@sorted_languages) {
    next unless $_->{'categoryinfo'}{'size'} > 1000;
    push @res, sprintf "%3d. %20s - %3d", $n++, $_->{'title'},
      $_->{'categoryinfo'}{'size'};
}

say join "\n", @res;

use Test::More;
ok @res == 13;
#ok @res >= 6 and @res < 10;
done_testing();

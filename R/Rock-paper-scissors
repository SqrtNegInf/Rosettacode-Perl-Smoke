#!/usr/local/bin/perl
#u# http://rosettacode.org/wiki/Rock-paper-scissors
#c# 2018-08-18 <RC
#p# OK

srand 123456;

use strict;
use warnings;
use feature 'say';

# not needed?
#use utf8;
#use open qw(:encoding(utf-8) :std);

#use 5.012;

use Getopt::Long;

my $output;

package Game {
    use List::Util qw(shuffle first);

    my $turns        = 0;
    my %human_choice = ( rock => 0, paper => 0, scissors => 0, );
    my %comp_choice  = ( rock => 0, paper => 0, scissors => 0, );
    my %what_beats =
      ( rock => 'paper', paper => 'scissors', scissors => 'rock', );
    my $comp_wins  = 0;
    my $human_wins = 0;
    my $draws      = 0;

    sub save_human_choice {
        my $ch = lc pop;
        if ( exists $human_choice{ $ch } ) {
            ++$human_choice{ $ch };
        }
        else {
            die __PACKAGE__ . ":: wrong choice: '$ch'";
        }
    }

    sub get_comp_choice {
        my @keys = sort keys %human_choice;  # for testing
        #my @keys = shuffle keys %human_choice;
        my $ch;
        my ( $prob, $rand ) = ( 0, rand );
        $ch = ( first { $rand <= ( $prob += ( $human_choice{ $_ } / $turns ) ) } @keys )
            if $turns > 0;
        $ch //= $keys[0];
        $ch = $what_beats{ $ch };
        ++$comp_choice{ $ch };
        return $ch;
    }

    sub make_turn {
        my ( $comp_ch, $human_ch ) = ( pop(), pop() );
        ++$turns;
        if ( $what_beats{ $human_ch } eq $comp_ch ) {
            ++$comp_wins;
            return 'I win!';
        }
        elsif ( $what_beats{ $comp_ch } eq $human_ch ) {
            ++$human_wins;
            return 'You win!';
        }
        else {
            ++$draws;
            return 'Draw!';
        }
    }

    sub get_final_report {
        my $report =
            "You chose:\n"
          . "  rock     = $human_choice{rock} times,\n"
          . "  paper    = $human_choice{paper} times,\n"
          . "  scissors = $human_choice{scissors} times,\n"
          . "I chose:\n"
          . "  rock     = $comp_choice{rock} times,\n"
          . "  paper    = $comp_choice{paper} times,\n"
          . "  scissors = $comp_choice{scissors} times,\n"
          . "Turns: $turns\n"
          . "I won: $comp_wins, you won: $human_wins, draws: $draws\n";
        return $report;
    }
}

sub main {
    GetOptions( 'quiet' => \my $quiet );
    greet() if !$quiet;
    while (1) {
        print_next_line() if !$quiet;
        my $input = get_input();
        last unless $input;
        if ( $input eq 'error' ) {
            $output .= "I don't understand!\n" if !$quiet;
            redo;
        }
        my $comp_choice = Game::get_comp_choice();
        Game::save_human_choice($input);
        my $result .= Game::make_turn( $input, $comp_choice );
        describe_turn_result( $input, $comp_choice, $result )
          if !$quiet;
    }
    $output .= Game::get_final_report();
}

sub greet {
    $output .= "Welcome to the Rock-Paper-Scissors game!\n"
      . "Choose 'rock', 'paper' or 'scissors'\n"
      . "Enter empty line or 'quit' to quit\n";
}

sub print_next_line {
    $output .= 'Your choice: ';
}

    sub get_input {
        my $input = <DATA>;
        $output .= "\n" and return if !$input;    # EOF
        chomp $input;
        return if !$input or $input =~ m/\A \s* q/xi;
        return 
            ( $input =~ m/\A \s* r/xi ) ? 'rock'
        : ( $input =~ m/\A \s* p/xi ) ? 'paper'
        : ( $input =~ m/\A \s* s/xi ) ? 'scissors'
        :                               'error';
    }

sub describe_turn_result {
    my ( $human_ch, $comp_ch, $result ) = @_;
    $output .= "You chose \u$human_ch, I chose \u$comp_ch. $result\n";
}

main();

#Example input can be generated as follows:
# perl -e '@c=qw(r p s); for(1..10000){ print $c[ rand() < 0.75 ? 0 : int rand(2) + 1 ], "\n" }' | perl rps.pl --quiet

say $output;

my $ref = <<'EOD';
Welcome to the Rock-Paper-Scissors game!
Choose 'rock', 'paper' or 'scissors'
Enter empty line or 'quit' to quit
Your choice: You chose Rock, I chose Scissors. You win!
Your choice: You chose Rock, I chose Paper. I win!
Your choice: You chose Rock, I chose Paper. I win!
Your choice: You chose Rock, I chose Paper. I win!
Your choice: You chose Scissors, I chose Paper. You win!
Your choice: You chose Rock, I chose Paper. I win!
Your choice: You chose Rock, I chose Paper. I win!
Your choice: You chose Rock, I chose Rock. Draw!
Your choice: You chose Rock, I chose Paper. I win!
Your choice: You chose Rock, I chose Rock. Draw!
Your choice: You chose Rock, I chose Paper. I win!
Your choice: You chose Rock, I chose Paper. I win!
Your choice: You chose Rock, I chose Paper. I win!
Your choice: You chose Rock, I chose Paper. I win!
Your choice: You chose Rock, I chose Paper. I win!
Your choice: You chose Rock, I chose Paper. I win!
Your choice: You chose Paper, I chose Paper. Draw!
Your choice: You chose Scissors, I chose Paper. You win!
Your choice: You chose Rock, I chose Paper. I win!
Your choice: You chose Rock, I chose Rock. Draw!
Your choice: 
You chose:
  rock     = 17 times,
  paper    = 1 times,
  scissors = 2 times,
I chose:
  rock     = 3 times,
  paper    = 16 times,
  scissors = 1 times,
Turns: 20
I won: 13, you won: 3, draws: 4
EOD

use Test::More;
is ($output, $ref);
done_testing();

__END__
r
r
r
r
s
r
r
r
r
r
r
r
r
r
r
r
p
s
r
r
